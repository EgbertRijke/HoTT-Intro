\begin{code}

{-# OPTIONS --without-K --allow-unsolved-metas #-}

module Lecture08 where

import Lecture07
open Lecture07 public

-- Section 8.1 Propositions

is-prop : {i : Level} (A : UU i) â†’ UU i
is-prop A = (x y : A) â†’ is-contr (Id x y)

is-prop-empty : is-prop empty
is-prop-empty ()

is-prop-unit : is-prop unit
is-prop-unit = is-prop-is-contr is-contr-unit

is-prop' : {i : Level} (A : UU i) â†’ UU i
is-prop' A = (x y : A) â†’ Id x y

is-prop-is-prop' : {i : Level} {A : UU i} â†’ is-prop' A â†’ is-prop A
is-prop-is-prop' {i} {A} H x y =
  dpair
    (concat _ (inv (H x x)) (H x y))
    (ind-Id x
      (Î» z p â†’ Id (concat _ (inv (H x x)) (H x z)) p)
      (left-inv (H x x)) y)

is-prop'-is-prop : {i : Level} {A : UU i} â†’ is-prop A â†’ is-prop' A
is-prop'-is-prop H x y = pr1 (H x y)

is-contr-is-prop-inh : {i : Level} {A : UU i} â†’ is-prop A â†’ A â†’ is-contr A
is-contr-is-prop-inh H a = dpair a (is-prop'-is-prop H a)

is-subtype : {i j : Level} {A : UU i} (B : A â†’ UU j) â†’ UU (i âŠ” j)
is-subtype B = (x : _) â†’ is-prop (B x)

-- Section 8.2 Sets

is-set : {i : Level} â†’ UU i â†’ UU i
is-set A = (x y : A) â†’ is-prop (Id x y)

axiom-K : {i : Level} â†’ UU i â†’ UU i
axiom-K A = (x : A) (p : Id x x) â†’ Id refl p

is-set-axiom-K : {i : Level} (A : UU i) â†’ axiom-K A â†’ is-set A
is-set-axiom-K A H x y =
  is-prop-is-prop' (ind-Id x (Î» z p â†’ (q : Id x z) â†’ Id p q) (H x) y)

axiom-K-is-set : {i : Level} (A : UU i) â†’ is-set A â†’ axiom-K A
axiom-K-is-set A H x p =
  concat
    (center (is-contr-is-prop-inh (H x x) refl))
      (inv (contraction (is-contr-is-prop-inh (H x x) refl) refl))
      (contraction (is-contr-is-prop-inh (H x x) refl) p)

is-equiv-prop-in-id : {i j : Level} {A : UU i}
  (R : A â†’ A â†’ UU j)
  (p : (x y : A) â†’ is-prop (R x y))
  (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ Id x y)
  â†’ (x y : A) â†’ is-equiv (i x y)
is-equiv-prop-in-id R p Ï i x =
  id-fundamental-retr x (i x)
    (Î» y â†’ dpair
      (ind-Id x (Î» z p â†’ R x z) (Ï x) y)
      ((Î» r â†’ is-prop'-is-prop (p x y) _ r)))

is-prop-is-equiv : {i j : Level} {A : UU i} (B : UU j)
  (f : A â†’ B) (E : is-equiv f) â†’ is-prop B â†’ is-prop A
is-prop-is-equiv B f E H x y =
  is-contr-is-equiv _ (ap f {x} {y}) (is-emb-is-equiv f E x y) (H (f x) (f y))

is-prop-is-equiv' : {i j : Level} (A : UU i) {B : UU j}
  (f : A â†’ B) (E : is-equiv f) â†’ is-prop A â†’ is-prop B
is-prop-is-equiv' A f E H =
  is-prop-is-equiv _ (inv-is-equiv E) (is-equiv-inv-is-equiv E) H

is-set-prop-in-id : {i j : Level} {A : UU i}
  (R : A â†’ A â†’ UU j)
  (p : (x y : A) â†’ is-prop (R x y))
  (Ï : (x : A) â†’ R x x)
  (i : (x y : A) â†’ R x y â†’ Id x y)
  â†’ is-set A
is-set-prop-in-id R p Ï i x y = is-prop-is-equiv' (R x y) (i x y) (is-equiv-prop-in-id R p Ï i x y) (p x y)

is-prop-Eq-â„• : (n m : â„•) â†’ is-prop (Eq-â„• n m)
is-prop-Eq-â„• zero-â„• zero-â„• = is-prop-unit
is-prop-Eq-â„• zero-â„• (succ-â„• m) = is-prop-empty
is-prop-Eq-â„• (succ-â„• n) zero-â„• = is-prop-empty
is-prop-Eq-â„• (succ-â„• n) (succ-â„• m) = is-prop-Eq-â„• n m

is-set-nat : is-set â„•
is-set-nat =
  is-set-prop-in-id
    Eq-â„•
    is-prop-Eq-â„•
    reflexive-Eq-â„•
    (least-reflexive-Eq-â„• (Î» n â†’ refl))

-- Section 8.3 General truncation levels

data ğ•‹ : U where
  neg-two-ğ•‹ : ğ•‹
  succ-ğ•‹ : ğ•‹ â†’ ğ•‹

neg-one-ğ•‹ : ğ•‹
neg-one-ğ•‹ = succ-ğ•‹ (neg-two-ğ•‹)

zero-ğ•‹ : ğ•‹
zero-ğ•‹ = succ-ğ•‹ (neg-one-ğ•‹)

one-ğ•‹ : ğ•‹
one-ğ•‹ = succ-ğ•‹ (zero-ğ•‹)

â„•-in-ğ•‹ : â„• â†’ ğ•‹
â„•-in-ğ•‹ zero-â„• = zero-ğ•‹
â„•-in-ğ•‹ (succ-â„• n) = succ-ğ•‹ (â„•-in-ğ•‹ n)

-- Probably it is better to define this where we first need it.
add-ğ•‹ : ğ•‹ â†’ ğ•‹ â†’ ğ•‹
add-ğ•‹ neg-two-ğ•‹ neg-two-ğ•‹ = neg-two-ğ•‹
add-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) = neg-two-ğ•‹
add-ğ•‹ neg-two-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ y)) = y
add-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) neg-two-ğ•‹ = neg-two-ğ•‹
add-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹) (succ-ğ•‹ y) = y
add-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ neg-two-ğ•‹)) y = y
add-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x))) y = succ-ğ•‹ (add-ğ•‹ (succ-ğ•‹ (succ-ğ•‹ x)) y)

is-trunc : {i : Level} (k : ğ•‹) â†’ UU i â†’ UU i
is-trunc neg-two-ğ•‹ A = is-contr A
is-trunc (succ-ğ•‹ k) A = (x y : A) â†’ is-trunc k (Id x y)
  
is-trunc-succ-is-trunc : {i : Level} (k : ğ•‹) (A : UU i) â†’
  is-trunc k A â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-succ-is-trunc neg-two-ğ•‹ A H = is-prop-is-contr H
is-trunc-succ-is-trunc (succ-ğ•‹ k) A H =
  Î» x y â†’ is-trunc-succ-is-trunc k (Id x y) (H x y)

is-trunc-is-equiv : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j}
  (f : A â†’ B) â†’ is-equiv f â†’ is-trunc k B â†’ is-trunc k A
is-trunc-is-equiv neg-two-ğ•‹ f is-equiv-f H =
  is-contr-is-equiv _ f is-equiv-f H
is-trunc-is-equiv (succ-ğ•‹ k) f is-equiv-f H x y =
  is-trunc-is-equiv k (ap f {x} {y})
    (is-emb-is-equiv f is-equiv-f x y) (H (f x) (f y))

is-trunc-is-equiv' : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j}
  (f : A â†’ B) â†’ is-equiv f â†’ is-trunc k A â†’ is-trunc k B
is-trunc-is-equiv' k f is-equiv-f is-trunc-A =
  is-trunc-is-equiv k
    ( inv-is-equiv is-equiv-f)
    ( is-equiv-inv-is-equiv is-equiv-f)
    ( is-trunc-A)

is-trunc-succ-is-emb : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j}
  (f : A â†’ B) â†’ is-emb f â†’ is-trunc (succ-ğ•‹ k) B â†’ is-trunc (succ-ğ•‹ k) A
is-trunc-succ-is-emb k f Ef H x y =
  is-trunc-is-equiv k (ap f {x} {y}) (Ef x y) (H (f x) (f y))

is-trunc-map : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j} â†’
  (A â†’ B) â†’ UU (i âŠ” j)
is-trunc-map k f = (y : _) â†’ is-trunc k (fib f y)

is-trunc-pr1-is-trunc-fam : {i j : Level} (k : ğ•‹) {A : UU i} (B : A â†’ UU j) â†’
  ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc-map k (pr1 {i} {j} {A} {B})
is-trunc-pr1-is-trunc-fam k B H x =
  is-trunc-is-equiv k
    ( fib-fam-fib-pr1 B x)
    ( is-equiv-fib-fam-fib-pr1 B x)
    ( H x)

is-trunc-fam-is-trunc-pr1 : {i j : Level} (k : ğ•‹) {A : UU i} (B : A â†’ UU j) â†’
  is-trunc-map k (pr1 {i} {j} {A} {B}) â†’ ((x : A) â†’ is-trunc k (B x))
is-trunc-fam-is-trunc-pr1 k B is-trunc-pr1 x =
  is-trunc-is-equiv k
    ( fib-pr1-fib-fam B x)
    ( is-equiv-fib-pr1-fib-fam B x)
    ( is-trunc-pr1 x)

is-trunc-map-is-trunc-ap : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j}
  (f : A â†’ B) â†’ ((x y : A) â†’ is-trunc-map k (ap f {x = x} {y = y})) â†’
  is-trunc-map (succ-ğ•‹ k) f
is-trunc-map-is-trunc-ap k f is-trunc-ap-f b (dpair x p) (dpair x' p') =
  is-trunc-is-equiv k
    ( fib-ap-eq-fib f (dpair x p) (dpair x' p'))
    ( is-equiv-fib-ap-eq-fib f (dpair x p) (dpair x' p'))
    ( is-trunc-ap-f x x' (concat _ p (inv p')))

is-trunc-ap-is-trunc-map : {i j : Level} (k : ğ•‹) {A : UU i} {B : UU j}
  (f : A â†’ B) â†’ is-trunc-map (succ-ğ•‹ k) f â†’
  (x y : A) â†’ is-trunc-map k (ap f {x = x} {y = y})
is-trunc-ap-is-trunc-map k f is-trunc-map-f x y p =
  is-trunc-is-equiv' k
    ( eq-fib-fib-ap f x y p)
    ( is-equiv-eq-fib-fib-ap f x y p)
    ( is-trunc-map-f (f y) (dpair x p) (dpair y refl))

is-prop-map : {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’ UU (i âŠ” j)
is-prop-map f = (b : _) â†’ is-trunc neg-one-ğ•‹ (fib f b)

is-emb-is-prop-map : {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’
  is-prop-map f â†’ is-emb f
is-emb-is-prop-map f is-prop-map-f x y =
  is-equiv-is-contr-map
    ( is-trunc-ap-is-trunc-map neg-two-ğ•‹ f is-prop-map-f x y)

is-prop-map-is-emb : {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’
  is-emb f â†’ is-prop-map f
is-prop-map-is-emb f is-emb-f =
  is-trunc-map-is-trunc-ap neg-two-ğ•‹ f
    ( Î» x y â†’ is-contr-map-is-equiv (is-emb-f x y))

is-emb-pr1-is-subtype : {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  is-subtype B â†’ is-emb (pr1 {B = B})
is-emb-pr1-is-subtype is-subtype-B =
  is-emb-is-prop-map pr1
    ( Î» x â†’ is-trunc-is-equiv neg-one-ğ•‹
      ( fib-fam-fib-pr1 _ x)
      ( is-equiv-fib-fam-fib-pr1 _ x)
      ( is-subtype-B x))

is-subtype-is-emb-pr1 : {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  is-emb (pr1 {B = B}) â†’ is-subtype B
is-subtype-is-emb-pr1 is-emb-pr1-B x =
  is-trunc-is-equiv neg-one-ğ•‹
    ( fib-pr1-fib-fam _ x)
    ( is-equiv-fib-pr1-fib-fam _ x)
    ( is-prop-map-is-emb pr1 is-emb-pr1-B x)

\end{code}
