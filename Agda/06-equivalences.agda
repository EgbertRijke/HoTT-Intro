{-# OPTIONS --without-K #-}

module 06-equivalences where

import 05-identity-types
open 05-identity-types public

-- Section 6.1 Homotopies

_~_ :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (f g : (x : A) â†’ B x) â†’ UU (i âŠ” j)
f ~ g = (x : _) â†’ Id (f x) (g x)

htpy-refl :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f : (x : A) â†’ B x} â†’ f ~ f
htpy-refl x = refl

{- Most of the time we get by with htpy-refl. However, sometimes Agda wants us
   to specify the implicit argument. The it is easier to call htpy-refl' than
   to use Agda's {f = ?} notation. -}
   
htpy-refl' :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (f : (x : A) â†’ B x) â†’ f ~ f
htpy-refl' f = htpy-refl

htpy-inv :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (f ~ g) â†’ (g ~ f)
htpy-inv H x = inv (H x)

_âˆ™h_ :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g h : (x : A) â†’ B x} â†’
  (f ~ g) â†’ (g ~ h) â†’ (f ~ h)
_âˆ™h_ H K x = (H x) âˆ™ (K x)

htpy-concat :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (f ~ g) â†’ (h : (x : A) â†’ B x) â†’ (g ~ h) â†’ (f ~ h)
htpy-concat H h K x = concat (H x) (h x) (K x)

htpy-assoc :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g h k : (x : A) â†’ B x} â†’
  (H : f ~ g) â†’ (K : g ~ h) â†’ (L : h ~ k) â†’
  ((H âˆ™h K) âˆ™h L) ~ (H âˆ™h (K âˆ™h L))
htpy-assoc H K L x = assoc (H x) (K x) (L x)

htpy-left-unit :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x}
  {H : f ~ g} â†’ (htpy-refl âˆ™h H) ~ H
htpy-left-unit x = left-unit

htpy-right-unit :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x}
  {H : f ~ g} â†’ (H âˆ™h htpy-refl) ~ H
htpy-right-unit x = right-unit

htpy-left-inv :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x}
  (H : f ~ g) â†’ ((htpy-inv H) âˆ™h H) ~ htpy-refl
htpy-left-inv H x = left-inv (H x)

htpy-right-inv :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x}
  (H : f ~ g) â†’ (H âˆ™h (htpy-inv H)) ~ htpy-refl
htpy-right-inv H x = right-inv (H x)

htpy-left-whisk :
  {i j k : Level} {A : UU i} {B : UU j} {C : UU k}
  (h : B â†’ C) {f g : A â†’ B} â†’ (f ~ g) â†’ ((h âˆ˜ f) ~ (h âˆ˜ g))
htpy-left-whisk h H x = ap h (H x)

_Â·l_ = htpy-left-whisk

htpy-right-whisk :
  {i j k : Level} {A : UU i} {B : UU j} {C : UU k}
  {g h : B â†’ C} (H : g ~ h) (f : A â†’ B) â†’ ((g âˆ˜ f) ~ (h âˆ˜ f))
htpy-right-whisk H f x = H (f x)

_Â·r_ = htpy-right-whisk

-- Section 6.2 Bi-invertible maps

sec :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’ UU (i âŠ” j)
sec {i} {j} {A} {B} f = Î£ (B â†’ A) (Î» g â†’ (f âˆ˜ g) ~ id)

retr :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’ UU (i âŠ” j)
retr {i} {j} {A} {B} f = Î£ (B â†’ A) (Î» g â†’ (g âˆ˜ f) ~ id)

_retract-of_ :
  {i j : Level} â†’ UU i â†’ UU j â†’ UU (i âŠ” j)
A retract-of B = Î£ (A â†’ B) retr

is-equiv :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’ UU (i âŠ” j)
is-equiv f = sec f Ã— retr f

_â‰ƒ_ :
  {i j : Level} (A : UU i) (B : UU j) â†’ UU (i âŠ” j)
A â‰ƒ B = Î£ (A â†’ B) (Î» f â†’ is-equiv f)

map-equiv :
  {i j : Level} {A : UU i} {B : UU j} â†’ (A â‰ƒ B) â†’ (A â†’ B)
map-equiv e = pr1 e

is-equiv-map-equiv :
  {i j : Level} {A : UU i} {B : UU j} (e : A â‰ƒ B) â†’ is-equiv (map-equiv e)
is-equiv-map-equiv e = pr2 e

has-inverse :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’ UU (i âŠ” j)
has-inverse {i} {j} {A} {B} f =
  Î£ (B â†’ A) (Î» g â†’ ((f âˆ˜ g) ~ id) Ã— ((g âˆ˜ f) ~ id))

abstract
  is-equiv-has-inverse' :
    {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’
    has-inverse f â†’ is-equiv f
  is-equiv-has-inverse' (pair g (pair H K)) = pair (pair g H) (pair g K)

abstract
  is-equiv-has-inverse :
    {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’
    (g : B â†’ A) (H : (f âˆ˜ g) ~ id) (K : (g âˆ˜ f) ~ id) â†’ is-equiv f
  is-equiv-has-inverse g H K =
    is-equiv-has-inverse' (pair g (pair H K))

{- We now show that if f is an equivalence, then it has an inverse. -}

htpy-section-retraction :
  { i j : Level} {A : UU i} {B : UU j} {f : A â†’ B}
  ( is-equiv-f : is-equiv f) â†’
  ( (pr1 (pr1 is-equiv-f))) ~ (pr1 (pr2 is-equiv-f))
htpy-section-retraction {i} {j} {A} {B} {f} (pair (pair g G) (pair h H)) =
    (htpy-inv (H Â·r g)) âˆ™h (h Â·l G)

has-inverse-is-equiv :
  {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’
  is-equiv f â†’ has-inverse f
has-inverse-is-equiv {i} {j} {A} {B} {f} (pair (pair g G) (pair h H)) =
  let is-equiv-f = pair (pair g G) (pair h H) in
  pair g (pair G (((htpy-section-retraction is-equiv-f) Â·r f) âˆ™h H))

inv-is-equiv :
  {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’ is-equiv f â†’ B â†’ A
inv-is-equiv is-equiv-f = pr1 (has-inverse-is-equiv is-equiv-f)

issec-inv-is-equiv :
  {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’
  (is-equiv-f : is-equiv f) â†’ (f âˆ˜ (inv-is-equiv is-equiv-f)) ~ id
issec-inv-is-equiv is-equiv-f = pr1 (pr2 (has-inverse-is-equiv is-equiv-f))

isretr-inv-is-equiv :
  {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’
  (is-equiv-f : is-equiv f) â†’ ((inv-is-equiv is-equiv-f) âˆ˜ f) ~ id
isretr-inv-is-equiv is-equiv-f = pr2 (pr2 (has-inverse-is-equiv is-equiv-f))

abstract
  is-equiv-inv-is-equiv :
    {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} â†’
    (is-equiv-f : is-equiv f) â†’ is-equiv (inv-is-equiv is-equiv-f)
  is-equiv-inv-is-equiv {i} {j} {A} {B} {f} is-equiv-f =
    is-equiv-has-inverse f
      ( isretr-inv-is-equiv is-equiv-f)
      ( issec-inv-is-equiv is-equiv-f)

inv-map-equiv :
  {i j : Level} {A : UU i} {B : UU j} â†’ (A â‰ƒ B) â†’ (B â†’ A)
inv-map-equiv e = inv-is-equiv (is-equiv-map-equiv e)

abstract
  is-equiv-inv-map-equiv :
    {i j : Level} {A : UU i} {B : UU j} (e : A â‰ƒ B) â†’ is-equiv (inv-map-equiv e)
  is-equiv-inv-map-equiv e =
    is-equiv-inv-is-equiv (is-equiv-map-equiv e)

inv-equiv :
  {i j : Level} {A : UU i} {B : UU j} â†’ (A â‰ƒ B) â†’ (B â‰ƒ A)
inv-equiv e = pair (inv-map-equiv e) (is-equiv-inv-map-equiv e)

is-equiv-id :
  {i : Level} (A : UU i) â†’ is-equiv (id {i} {A})
is-equiv-id A = pair (pair id htpy-refl) (pair id htpy-refl)

equiv-id :
  {i : Level} (A : UU i) â†’ A â‰ƒ A
equiv-id A = pair id (is-equiv-id A)

inv-Î -swap :
  {i j k : Level} {A : UU i} {B : UU j} (C : A â†’ B â†’ UU k) â†’
  ((y : B) (x : A) â†’ C x y) â†’ ((x : A) (y : B) â†’ C x y)
inv-Î -swap C g x y = g y x

abstract
  is-equiv-Î -swap :
    {i j k : Level} {A : UU i} {B : UU j} (C : A â†’ B â†’ UU k) â†’
    is-equiv (Î -swap {i} {j} {k} {A} {B} {C})
  is-equiv-Î -swap C =
    is-equiv-has-inverse
      ( inv-Î -swap C)
      ( htpy-refl)
      ( htpy-refl)

-- Section 6.3 The identity type of a Î£-type

Eq-Î£ :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (s t : Î£ A B) â†’ UU (i âŠ” j)
Eq-Î£ {B = B} s t = Î£ (Id (pr1 s) (pr1 t)) (Î» Î± â†’ Id (tr B Î± (pr2 s)) (pr2 t))

reflexive-Eq-Î£ :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (s : Î£ A B) â†’ Eq-Î£ s s
reflexive-Eq-Î£ (pair a b) = pair refl refl

pair-eq :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {s t : Î£ A B} â†’
  (Id s t) â†’ Eq-Î£ s t
pair-eq {s = s} refl = reflexive-Eq-Î£ s

eq-pair :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {s t : Î£ A B} â†’
  (Î± : Id (pr1 s) (pr1 t)) â†’ Id (tr B Î± (pr2 s)) (pr2 t) â†’ Id s t
eq-pair {B = B} {pair x y} {pair .x .y} refl refl = refl

eq-pair' :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {s t : Î£ A B} â†’
  Eq-Î£ s t â†’ Id s t
eq-pair' (pair Î± Î²) = eq-pair Î± Î²

isretr-pair-eq :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (s t : Î£ A B) â†’
  ((pair-eq {s = s} {t}) âˆ˜ (eq-pair' {s = s} {t})) ~ id {A = Eq-Î£ s t}
isretr-pair-eq (pair x y) (pair .x .y) (pair refl refl) = refl

issec-pair-eq :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (s t : Î£ A B) â†’
  ((eq-pair' {s = s} {t}) âˆ˜ (pair-eq {s = s} {t})) ~ id
issec-pair-eq (pair x y) .(pair x y) refl = refl

abstract
  is-equiv-eq-pair :
    {i j : Level} {A : UU i} {B : A â†’ UU j} (s t : Î£ A B) â†’
    is-equiv (eq-pair' {s = s} {t})
  is-equiv-eq-pair s t =
    is-equiv-has-inverse
      ( pair-eq)
      ( issec-pair-eq s t)
      ( isretr-pair-eq s t)

abstract
  is-equiv-pair-eq :
    {i j : Level} {A : UU i} {B : A â†’ UU j} (s t : Î£ A B) â†’
    is-equiv (pair-eq {s = s} {t})
  is-equiv-pair-eq s t =
    is-equiv-has-inverse
      ( eq-pair')
      ( isretr-pair-eq s t)
      ( issec-pair-eq s t)

{- We also define a function eq-pair-triv, which is like eq-pair but simplified 
   for the case where B is just a type. -}

Eq-prod :
  {i j : Level} {A : UU i} {B : UU j} (s t : A Ã— B) â†’ UU (i âŠ” j)
Eq-prod s t = (Id (pr1 s) (pr1 t)) Ã— (Id (pr2 s) (pr2 t))

eq-pair-triv' :
  {i j : Level} {A : UU i} {B : UU j} (s t : prod A B) â†’
  Eq-prod s t â†’ Id s t
eq-pair-triv' (pair x y) (pair .x .y) (pair refl refl) = refl

eq-pair-triv :
  {i j : Level} {A : UU i} {B : UU j} {s t : prod A B} â†’
  Eq-prod s t â†’ Id s t
eq-pair-triv {s = s} {t} = eq-pair-triv' s t

{- Ideally, we would use the 3-for-2 property of equivalences to show that 
   eq-pair-triv is an equivalence, using that eq-pair is an equivalence. 
   Indeed, there is an equivalence 
   
     (Id x x') Ã— (Id y y') â†’ Î£ (Id x x') (Î» p â†’ Id (tr (Î» x â†’ B) p y) y'). 

   However, to show that this map is an equivalence we either give a direct 
   proof (in which case we might as well have given a direct proof that 
   eq-pair-triv is an equivalence), or we use the fact that it is the induced 
   map on total spaces of a fiberwise equivalence (the topic of Lecture 8). 
   Thus it seems that a direct proof showing that eq-pair-triv is an 
   equivalence is quickest for now. -}

pair-eq-triv' :
  {i j : Level} {A : UU i} {B : UU j} (s t : prod A B) â†’
  Id s t â†’ Eq-prod s t
pair-eq-triv' s t Î± = pair (ap pr1 Î±) (ap pr2 Î±)

isretr-pair-eq-triv' :
  {i j : Level} {A : UU i} {B : UU j} (s t : prod A B) â†’
  ((pair-eq-triv' s t) âˆ˜ (eq-pair-triv' s t)) ~ id
isretr-pair-eq-triv' (pair x y) (pair .x .y) (pair refl refl) = refl

issec-pair-eq-triv' :
  {i j : Level} {A : UU i} {B : UU j} (s t : prod A B) â†’
  ((eq-pair-triv' s t) âˆ˜ (pair-eq-triv' s t)) ~ id
issec-pair-eq-triv' (pair x y) (pair .x .y) refl = refl

abstract
  is-equiv-eq-pair-triv' :
    {i j : Level} {A : UU i} {B : UU j} (s t : prod A B) â†’
    is-equiv (eq-pair-triv' s t)
  is-equiv-eq-pair-triv' s t =
    is-equiv-has-inverse
      ( pair-eq-triv' s t)
      ( issec-pair-eq-triv' s t)
      ( isretr-pair-eq-triv' s t)

-- Exercises

-- Exercise 6.1

element :
  {i : Level} {A : UU i} â†’ A â†’ unit â†’ A
element a star = a

htpy-element-constant :
  {i : Level} {A : UU i} (a : A) â†’
  (element a) ~ (const unit A a)
htpy-element-constant a star = refl

-- Exercise 6.2

ap-const :
  {i j : Level} {A : UU i} {B : UU j} (b : B) (x y : A) â†’
  (ap (const A B b) {x} {y}) ~ const (Id x y) (Id b b) refl
ap-const b x .x refl = refl

-- Exercise 6.3

inv-inv :
  {i : Level} {A : UU i} {x y : A} (p : Id x y) â†’ Id (inv (inv p)) p
inv-inv refl = refl

abstract
  is-equiv-inv :
    {i : Level} {A : UU i} (x y : A) â†’
    is-equiv (Î» (p : Id x y) â†’ inv p)
  is-equiv-inv x y =
    is-equiv-has-inverse inv inv-inv inv-inv

equiv-inv :
  {i : Level} {A : UU i} (x y : A) â†’ (Id x y) â‰ƒ (Id y x)
equiv-inv x y = pair inv (is-equiv-inv x y)

inv-concat :
  {i : Level} {A : UU i} {x y : A} (p : Id x y) (z : A) â†’
  (Id x z) â†’ (Id y z)
inv-concat p = concat (inv p)

isretr-inv-concat :
  {i : Level} {A : UU i} {x y : A} (p : Id x y) (z : A) â†’
  ((inv-concat p z) âˆ˜ (concat p z)) ~ id
isretr-inv-concat refl z q = refl

issec-inv-concat :
  {i : Level} {A : UU i} {x y : A} (p : Id x y) (z : A) â†’
  ((concat p z) âˆ˜ (inv-concat p z)) ~ id
issec-inv-concat refl z refl = refl

abstract
  is-equiv-concat :
    {i : Level} {A : UU i} {x y : A} (p : Id x y) (z : A) â†’
    is-equiv (concat p z)
  is-equiv-concat p z =
    is-equiv-has-inverse
      ( inv-concat p z)
      ( issec-inv-concat p z)
      ( isretr-inv-concat p z)

equiv-concat :
  {i : Level} {A : UU i} {x y : A} (p : Id x y) (z : A) â†’
  Id y z â‰ƒ Id x z
equiv-concat p z = pair (concat p z) (is-equiv-concat p z)

concat' :
  {i : Level} {A : UU i} (x : A) {y z : A} â†’ Id y z â†’ Id x y â†’ Id x z
concat' x q p = p âˆ™ q

inv-concat' :
  {i : Level} {A : UU i} (x : A) {y z : A} â†’ Id y z â†’
  Id x z â†’ Id x y
inv-concat' x q = concat' x (inv q)

isretr-inv-concat' :
  {i : Level} {A : UU i} (x : A) {y z : A} (q : Id y z) â†’
  ((inv-concat' x q) âˆ˜ (concat' x q)) ~ id
isretr-inv-concat' x refl refl = refl

issec-inv-concat' :
  {i : Level} {A : UU i} (x : A) {y z : A} (q : Id y z) â†’
  ((concat' x q) âˆ˜ (inv-concat' x q)) ~ id
issec-inv-concat' x refl refl = refl

abstract
  is-equiv-concat' :
    {i : Level} {A : UU i} (x : A) {y z : A} (q : Id y z) â†’
    is-equiv (concat' x q)
  is-equiv-concat' x q =
    is-equiv-has-inverse
      ( inv-concat' x q)
      ( issec-inv-concat' x q)
      ( isretr-inv-concat' x q)

equiv-concat' :
  {i : Level} {A : UU i} (x : A) {y z : A} (q : Id y z) â†’
  Id x y â‰ƒ Id x z
equiv-concat' x q = pair (concat' x q) (is-equiv-concat' x q)

inv-tr :
  {i j : Level} {A : UU i} (B : A â†’ UU j) {x y : A} â†’
  Id x y â†’ B y â†’ B x
inv-tr B p = tr B (inv p)

isretr-inv-tr :
  {i j : Level} {A : UU i} (B : A â†’ UU j) {x y : A}
  (p : Id x y) â†’ ((inv-tr B p ) âˆ˜ (tr B p)) ~ id
isretr-inv-tr B refl b = refl

issec-inv-tr :
  {i j : Level} {A : UU i} (B : A â†’ UU j) {x y : A}
  (p : Id x y) â†’ ((tr B p) âˆ˜ (inv-tr B p)) ~ id
issec-inv-tr B refl b = refl

abstract
  is-equiv-tr :
    {i j : Level} {A : UU i} (B : A â†’ UU j) {x y : A}
    (p : Id x y) â†’ is-equiv (tr B p)
  is-equiv-tr B p =
    is-equiv-has-inverse
      ( inv-tr B p)
      ( issec-inv-tr B p)
      ( isretr-inv-tr B p)

equiv-tr :
  {i j : Level} {A : UU i} (B : A â†’ UU j) {x y : A}
  (p : Id x y) â†’ (B x) â‰ƒ (B y)
equiv-tr B p = pair (tr B p) (is-equiv-tr B p)

-- Exercise 6.4

abstract
  is-equiv-htpy :
    {i j : Level} {A : UU i} {B : UU j} {f : A â†’ B} (g : A â†’ B) â†’
    f ~ g â†’ is-equiv g â†’ is-equiv f
  is-equiv-htpy g H (pair (pair gs issec) (pair gr isretr)) =
    pair
      ( pair gs ((H Â·r gs) âˆ™h issec))
      ( pair gr ((gr Â·l H) âˆ™h isretr))

abstract
  is-equiv-htpy' :
    {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) {g : A â†’ B} â†’
    f ~ g â†’ is-equiv f â†’ is-equiv g
  is-equiv-htpy' f H = is-equiv-htpy f (htpy-inv H)

htpy-inv-is-equiv :
  {i j : Level} {A : UU i} {B : UU j} {f f' : A â†’ B} (H : f ~ f') â†’
  (is-equiv-f : is-equiv f) (is-equiv-f' : is-equiv f') â†’
  (inv-is-equiv is-equiv-f) ~ (inv-is-equiv is-equiv-f')
htpy-inv-is-equiv H is-equiv-f is-equiv-f' b =
  ( inv (isretr-inv-is-equiv is-equiv-f' (inv-is-equiv is-equiv-f b))) âˆ™
  ( ap (inv-is-equiv is-equiv-f')
    ( ( inv (H (inv-is-equiv is-equiv-f b))) âˆ™
      ( issec-inv-is-equiv is-equiv-f b)))

-- Exercise 6.5

{- Exercise 6.5 (a) asks to show that, given a commuting triangle f ~ g âˆ˜ h and
   a section s of h, we get a new commuting triangle g ~ f âˆ˜ s. Moreover, under
   the same assumptions it follows that f has a section if and only if g has a 
   section. -}

triangle-section :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) (S : sec h) â†’
  g ~ (f âˆ˜ (pr1 S))
triangle-section f g h H (pair s issec) =
  htpy-inv (( H Â·r s) âˆ™h (g Â·l issec))

section-comp :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  sec h â†’ sec f â†’ sec g
section-comp f g h H sec-h sec-f =
  pair (h âˆ˜ (pr1 sec-f)) ((htpy-inv (H Â·r (pr1 sec-f))) âˆ™h (pr2 sec-f))

section-comp' :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  sec h â†’ sec g â†’ sec f
section-comp' f g h H sec-h sec-g =
  pair
    ( (pr1 sec-h) âˆ˜ (pr1 sec-g))
    ( ( H Â·r ((pr1 sec-h) âˆ˜ (pr1 sec-g))) âˆ™h
      ( ( g Â·l ((pr2 sec-h) Â·r (pr1 sec-g))) âˆ™h ((pr2 sec-g))))

{- Exercise 6.5 (b) is dual to exercise 5.5 (a). It asks to show that, given a 
   commuting triangle f ~ g âˆ˜ h and a retraction r of g, we get a new commuting
   triangle h ~ r âˆ˜ f. Moreover, under these assumptions it also follows that f
   has a retraction if and only if h has a retraction. -}

triangle-retraction :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) (R : retr g) â†’
  h ~ ((pr1 R) âˆ˜ f)
triangle-retraction f g h H (pair r isretr) =
  htpy-inv (( r Â·l H) âˆ™h (isretr Â·r h))

retraction-comp :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  retr g â†’ retr f â†’ retr h
retraction-comp f g h H retr-g retr-f =
  pair
    ( (pr1 retr-f) âˆ˜ g)
    ( (htpy-inv ((pr1 retr-f) Â·l H)) âˆ™h (pr2 retr-f))

retraction-comp' :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  retr g â†’ retr h â†’ retr f
retraction-comp' f g h H retr-g retr-h =
  pair
    ( (pr1 retr-h) âˆ˜ (pr1 retr-g))
    ( ( ((pr1 retr-h) âˆ˜ (pr1 retr-g)) Â·l H) âˆ™h
      ( ((pr1 retr-h) Â·l ((pr2 retr-g) Â·r h)) âˆ™h (pr2 retr-h)))

{- In Exercise 6.5 (c) we use the constructions of parts (a) and (b) to derive 
   the 3-for-2 property of equivalences. -}

abstract
  is-equiv-comp :
    {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
    (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
    is-equiv h â†’ is-equiv g â†’ is-equiv f
  is-equiv-comp f g h H (pair sec-h retr-h) (pair sec-g retr-g) =
    pair
      ( section-comp' f g h H sec-h sec-g)
      ( retraction-comp' f g h H retr-g retr-h)

abstract
  is-equiv-comp' :
    {i j k : Level} {A : UU i} {B : UU j} {X : UU k} (g : B â†’ X) (h : A â†’ B) â†’
    is-equiv h â†’ is-equiv g â†’ is-equiv (g âˆ˜ h)
  is-equiv-comp' g h = is-equiv-comp (g âˆ˜ h) g h htpy-refl

equiv-comp :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k} â†’
  (B â‰ƒ X) â†’ (A â‰ƒ B) â†’ (A â‰ƒ X)
equiv-comp g h =
  pair ((pr1 g) âˆ˜ (pr1 h)) (is-equiv-comp' (pr1 g) (pr1 h) (pr2 h) (pr2 g))

_âˆ˜e_ :
  {i j k : Level} {A : UU i} {B : UU j} {X : UU k} â†’
  (B â‰ƒ X) â†’ (A â‰ƒ B) â†’ (A â‰ƒ X)
_âˆ˜e_ = equiv-comp

abstract
  is-equiv-left-factor :
    {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
    (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
    is-equiv f â†’ is-equiv h â†’ is-equiv g
  is-equiv-left-factor f g h H
    ( pair sec-f retr-f)
    ( pair (pair sh sh-issec) retr-h) =
    pair
      ( section-comp f g h H (pair sh sh-issec) sec-f)
      ( retraction-comp' g f sh
        ( triangle-section f g h H (pair sh sh-issec))
        ( retr-f)
        ( pair h sh-issec))

abstract
  is-equiv-left-factor' :
    {i j k : Level} {A : UU i} {B : UU j} {X : UU k} (g : B â†’ X) (h : A â†’ B) â†’
    is-equiv (g âˆ˜ h) â†’ is-equiv h â†’ is-equiv g
  is-equiv-left-factor' g h =
    is-equiv-left-factor (g âˆ˜ h) g h htpy-refl

abstract
  is-equiv-right-factor :
    {i j k : Level} {A : UU i} {B : UU j} {X : UU k}
    (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
    is-equiv g â†’ is-equiv f â†’ is-equiv h
  is-equiv-right-factor f g h H
    ( pair sec-g (pair rg rg-isretr))
    ( pair sec-f retr-f) =
    pair
      ( section-comp' h rg f
        ( triangle-retraction f g h H (pair rg rg-isretr))
        ( sec-f)
        ( pair g rg-isretr))
      ( retraction-comp f g h H (pair rg rg-isretr) retr-f)

abstract
  is-equiv-right-factor' :
    {i j k : Level} {A : UU i} {B : UU j} {X : UU k} (g : B â†’ X) (h : A â†’ B) â†’ 
    is-equiv g â†’ is-equiv (g âˆ˜ h) â†’ is-equiv h
  is-equiv-right-factor' g h =
    is-equiv-right-factor (g âˆ˜ h) g h htpy-refl

-- Exercise 6.6

{- In this exercise we show that the negation function on the booleans is an 
   equivalence. Moreover, we show that any constant function on the booleans is
   not an equivalence. -}

neg-ğŸš : bool â†’ bool
neg-ğŸš true = false
neg-ğŸš false = true

neg-neg-ğŸš : (neg-ğŸš âˆ˜ neg-ğŸš) ~ id
neg-neg-ğŸš true = refl
neg-neg-ğŸš false = refl

abstract
  is-equiv-neg-ğŸš : is-equiv neg-ğŸš
  is-equiv-neg-ğŸš = is-equiv-has-inverse neg-ğŸš neg-neg-ğŸš neg-neg-ğŸš

equiv-neg-ğŸš : bool â‰ƒ bool
equiv-neg-ğŸš = pair neg-ğŸš is-equiv-neg-ğŸš

abstract
  not-true-is-false : Â¬ (Id true false)
  not-true-is-false p =
    tr (Eq-ğŸš true) p (reflexive-Eq-ğŸš true) 

abstract
  not-equiv-const :
    (b : bool) â†’ Â¬ (is-equiv (const bool bool b))
  not-equiv-const true (pair (pair s issec) (pair r isretr)) =
    not-true-is-false (issec false)
  not-equiv-const false (pair (pair s issec) (pair r isretr)) =
    not-true-is-false (inv (issec true))

-- Exercise 6.7

{- In this exercise we show that the successor function on the integers is an
   equivalence. -}

abstract
  left-inverse-pred-â„¤ :
    (k : â„¤) â†’ Id (pred-â„¤ (succ-â„¤ k)) k
  left-inverse-pred-â„¤ (inl zero-â„•) = refl
  left-inverse-pred-â„¤ (inl (succ-â„• x)) = refl
  left-inverse-pred-â„¤ (inr (inl star)) = refl
  left-inverse-pred-â„¤ (inr (inr zero-â„•)) = refl
  left-inverse-pred-â„¤ (inr (inr (succ-â„• x))) = refl
  
  right-inverse-pred-â„¤ :
    (k : â„¤) â†’ Id (succ-â„¤ (pred-â„¤ k)) k
  right-inverse-pred-â„¤ (inl zero-â„•) = refl
  right-inverse-pred-â„¤ (inl (succ-â„• x)) = refl
  right-inverse-pred-â„¤ (inr (inl star)) = refl
  right-inverse-pred-â„¤ (inr (inr zero-â„•)) = refl
  right-inverse-pred-â„¤ (inr (inr (succ-â„• x))) = refl
  
  is-equiv-succ-â„¤ : is-equiv succ-â„¤
  is-equiv-succ-â„¤ =
    is-equiv-has-inverse pred-â„¤ right-inverse-pred-â„¤ left-inverse-pred-â„¤
  
equiv-succ-â„¤ : â„¤ â‰ƒ â„¤
equiv-succ-â„¤ = pair succ-â„¤ is-equiv-succ-â„¤

-- Exercise 6.8

{- In this exercise we construct an equivalence from A + B to B + A, showing 
   that the coproduct is commutative. -}

swap-coprod :
  {i j : Level} (A : UU i) (B : UU j) â†’ coprod A B â†’ coprod B A
swap-coprod A B (inl x) = inr x
swap-coprod A B (inr x) = inl x

swap-swap-coprod :
  {i j : Level} (A : UU i) (B : UU j) â†’
  ((swap-coprod B A) âˆ˜ (swap-coprod A B)) ~ id
swap-swap-coprod A B (inl x) = refl
swap-swap-coprod A B (inr x) = refl

abstract
  is-equiv-swap-coprod :
    {i j : Level} (A : UU i) (B : UU j) â†’ is-equiv (swap-coprod A B)
  is-equiv-swap-coprod A B =
    is-equiv-has-inverse
      ( swap-coprod B A)
      ( swap-swap-coprod B A)
      ( swap-swap-coprod A B)

equiv-swap-coprod :
  {i j : Level} (A : UU i) (B : UU j) â†’ coprod A B â‰ƒ coprod B A
equiv-swap-coprod A B = pair (swap-coprod A B) (is-equiv-swap-coprod A B)

swap-prod :
  {i j : Level} (A : UU i) (B : UU j) â†’ prod A B â†’ prod B A
swap-prod A B t = pair (pr2 t) (pr1 t)

swap-swap-prod :
  {i j : Level} (A : UU i) (B : UU j) â†’
  ((swap-prod B A) âˆ˜ (swap-prod A B)) ~ id
swap-swap-prod A B (pair x y) = refl

abstract
  is-equiv-swap-prod :
    {i j : Level} (A : UU i) (B : UU j) â†’
    is-equiv (swap-prod A B)
  is-equiv-swap-prod A B =
    is-equiv-has-inverse
      ( swap-prod B A)
      ( swap-swap-prod B A)
      ( swap-swap-prod A B)

equiv-swap-prod :
  {i j : Level} (A : UU i) (B : UU j) â†’ (A Ã— B) â‰ƒ (B Ã— A)
equiv-swap-prod A B = pair (swap-prod A B) (is-equiv-swap-prod A B)

-- Exercise 6.9

{- In this exercise we show that if A is a retract of B, then so are its 
   identity types. -}

ap-retraction :
  {i j : Level} {A : UU i} {B : UU j}
  (i : A â†’ B) (r : B â†’ A) (H : (r âˆ˜ i) ~ id)
  (x y : A) â†’ Id (i x) (i y) â†’ Id x y
ap-retraction i r H x y p =
    ( inv (H x)) âˆ™ ((ap r p) âˆ™ (H y))

isretr-ap-retraction :
  {i j : Level} {A : UU i} {B : UU j}
  (i : A â†’ B) (r : B â†’ A) (H : (r âˆ˜ i) ~ id)
  (x y : A) â†’ ((ap-retraction i r H x y) âˆ˜ (ap i {x} {y})) ~ id
isretr-ap-retraction i r H x .x refl = left-inv (H x)

retr-ap :
  {i j : Level} {A : UU i} {B : UU j} (i : A â†’ B) â†’
  retr i â†’ (x y : A) â†’ retr (ap i {x} {y})
retr-ap i (pair r H) x y =
  pair (ap-retraction i r H x y) (isretr-ap-retraction i r H x y)

Id-retract-of-Id :
  {i j : Level} {A : UU i} {B : UU j} (R : A retract-of B) â†’
  (x y : A) â†’ (Id x y) retract-of (Id (pr1 R x) (pr1 R y))
Id-retract-of-Id (pair i (pair r H)) x y =
  pair
    ( ap i {x} {y})
    ( retr-ap i (pair r H) x y)

-- Exercise 6.10

Î£-assoc :
  {i j k : Level} (A : UU i) (B : A â†’ UU j) (C : (Î£ A B) â†’ UU k) â†’
  Î£ (Î£ A B) C â†’ Î£ A (Î» x â†’ Î£ (B x) (Î» y â†’ C (pair x y)))
Î£-assoc A B C (pair (pair x y) z) = pair x (pair y z)

Î£-assoc' :
  {i j k : Level} (A : UU i) (B : A â†’ UU j) (C : (Î£ A B) â†’ UU k) â†’
  Î£ A (Î» x â†’ Î£ (B x) (Î» y â†’ C (pair x y))) â†’ Î£ (Î£ A B) C
Î£-assoc' A B C t = pair (pair (pr1 t) (pr1 (pr2 t))) (pr2 (pr2 t))

Î£-assoc-assoc :
  {i j k : Level} (A : UU i) (B : A â†’ UU j)
  (C : (Î£ A B) â†’ UU k) â†’ ((Î£-assoc' A B C) âˆ˜ (Î£-assoc A B C)) ~ id
Î£-assoc-assoc A B C (pair (pair x y) z) = refl

Î£-assoc-assoc' :
  {i j k : Level} (A : UU i) (B : A â†’ UU j)
  (C : (Î£ A B) â†’ UU k) â†’ ((Î£-assoc A B C) âˆ˜ (Î£-assoc' A B C)) ~ id
Î£-assoc-assoc' A B C (pair x (pair y z)) = refl

abstract
  is-equiv-Î£-assoc :
    {i j k : Level} (A : UU i) (B : A â†’ UU j)
    (C : (Î£ A B) â†’ UU k) â†’ is-equiv (Î£-assoc A B C)
  is-equiv-Î£-assoc A B C =
    is-equiv-has-inverse
      ( Î£-assoc' A B C)
      ( Î£-assoc-assoc' A B C)
      ( Î£-assoc-assoc A B C)

-- Exercise 6.11

Î£-swap :
  {i j k : Level} (A : UU i) (B : UU j) (C : A â†’ B â†’ UU k) â†’
  Î£ A (Î» x â†’ Î£ B (C x)) â†’ Î£ B (Î» y â†’ Î£ A (Î» x â†’ C x y))
Î£-swap A B C t = pair (pr1 (pr2 t)) (pair (pr1 t) (pr2 (pr2 t)))

Î£-swap' :
  {i j k : Level} (A : UU i) (B : UU j) (C : A â†’ B â†’ UU k) â†’
  Î£ B (Î» y â†’ Î£ A (Î» x â†’ C x y)) â†’ Î£ A (Î» x â†’ Î£ B (C x))
Î£-swap' A B C = Î£-swap B A (Î» y x â†’ C x y)

Î£-swap-swap :
  {i j k : Level} (A : UU i) (B : UU j) (C : A â†’ B â†’ UU k) â†’
  ((Î£-swap' A B C) âˆ˜ (Î£-swap A B C)) ~ id
Î£-swap-swap A B C (pair x (pair y z)) = refl

abstract
  is-equiv-Î£-swap :
    {i j k : Level} (A : UU i) (B : UU j) (C : A â†’ B â†’ UU k) â†’
    is-equiv (Î£-swap A B C)
  is-equiv-Î£-swap A B C =
    is-equiv-has-inverse
      ( Î£-swap' A B C)
      ( Î£-swap-swap B A (Î» y x â†’ C x y))
      ( Î£-swap-swap A B C)

-- Exercise 6.12

{- Exercise 6.12 (a) simply asks to prove the unit laws. The left unit law 
   holds by judgmental equality. -}

abstract
  left-unit-law-add-â„¤ :
    (k : â„¤) â†’ Id (add-â„¤ zero-â„¤ k) k
  left-unit-law-add-â„¤ k = refl
  
  right-unit-law-add-â„¤ :
    (k : â„¤) â†’ Id (add-â„¤ k zero-â„¤) k
  right-unit-law-add-â„¤ (inl zero-â„•) = refl
  right-unit-law-add-â„¤ (inl (succ-â„• x)) =
    ap pred-â„¤ (right-unit-law-add-â„¤ (inl x))
  right-unit-law-add-â„¤ (inr (inl star)) = refl
  right-unit-law-add-â„¤ (inr (inr zero-â„•)) = refl
  right-unit-law-add-â„¤ (inr (inr (succ-â„• x))) =
    ap succ-â„¤ (right-unit-law-add-â„¤ (inr (inr x)))

{- Exercise 6.12 (b) asks to show the left and right predecessor and successor 
   laws. These are helpful to give proofs of associativity and commutativity. 
   -}

abstract
  left-predecessor-law-add-â„¤ :
    (x y : â„¤) â†’ Id (add-â„¤ (pred-â„¤ x) y) (pred-â„¤ (add-â„¤ x y))
  left-predecessor-law-add-â„¤ (inl n) y = refl
  left-predecessor-law-add-â„¤ (inr (inl star)) y = refl
  left-predecessor-law-add-â„¤ (inr (inr zero-â„•)) y =
    ( ap (Î» t â†’ add-â„¤ t y) (left-inverse-pred-â„¤ zero-â„¤)) âˆ™ 
    ( inv (left-inverse-pred-â„¤ y))
  left-predecessor-law-add-â„¤ (inr (inr (succ-â„• x))) y =
    ( ap (Î» t â†’ (add-â„¤ t y)) (left-inverse-pred-â„¤ (inr (inr x)))) âˆ™
    ( inv (left-inverse-pred-â„¤ (add-â„¤ (inr (inr x)) y)))

  right-predecessor-law-add-â„¤ :
    (x y : â„¤) â†’ Id (add-â„¤ x (pred-â„¤ y)) (pred-â„¤ (add-â„¤ x y))
  right-predecessor-law-add-â„¤ (inl zero-â„•) n = refl
  right-predecessor-law-add-â„¤ (inl (succ-â„• m)) n =
    ap pred-â„¤ (right-predecessor-law-add-â„¤ (inl m) n)
  right-predecessor-law-add-â„¤ (inr (inl star)) n = refl
  right-predecessor-law-add-â„¤ (inr (inr zero-â„•)) n =
    (right-inverse-pred-â„¤ n) âˆ™ (inv (left-inverse-pred-â„¤ n))
  right-predecessor-law-add-â„¤ (inr (inr (succ-â„• x))) n =
    ( ap succ-â„¤ (right-predecessor-law-add-â„¤ (inr (inr x)) n)) âˆ™
    ( ( right-inverse-pred-â„¤ (add-â„¤ (inr (inr x)) n)) âˆ™ 
      ( inv (left-inverse-pred-â„¤ (add-â„¤ (inr (inr x)) n))))

abstract
  left-successor-law-add-â„¤ :
    (x y : â„¤) â†’ Id (add-â„¤ (succ-â„¤ x) y) (succ-â„¤ (add-â„¤ x y))
  left-successor-law-add-â„¤ (inl zero-â„•) y =
    ( ap (Î» t â†’ add-â„¤ t y) (right-inverse-pred-â„¤ zero-â„¤)) âˆ™
    ( inv (right-inverse-pred-â„¤ y))
  left-successor-law-add-â„¤ (inl (succ-â„• x)) y =
    ( inv (right-inverse-pred-â„¤ (add-â„¤ (inl x) y))) âˆ™
    ( ap succ-â„¤ (inv (left-predecessor-law-add-â„¤ (inl x) y)))
  left-successor-law-add-â„¤ (inr (inl star)) y = refl
  left-successor-law-add-â„¤ (inr (inr x)) y = refl

  right-successor-law-add-â„¤ :
    (x y : â„¤) â†’ Id (add-â„¤ x (succ-â„¤ y)) (succ-â„¤ (add-â„¤ x y))
  right-successor-law-add-â„¤ (inl zero-â„•) y =
    (left-inverse-pred-â„¤ y) âˆ™ (inv (right-inverse-pred-â„¤ y))
  right-successor-law-add-â„¤ (inl (succ-â„• x)) y =
    ( ap pred-â„¤ (right-successor-law-add-â„¤ (inl x) y)) âˆ™
    ( ( left-inverse-pred-â„¤ (add-â„¤ (inl x) y)) âˆ™
      ( inv (right-inverse-pred-â„¤ (add-â„¤ (inl x) y))))
  right-successor-law-add-â„¤ (inr (inl star)) y = refl
  right-successor-law-add-â„¤ (inr (inr zero-â„•)) y = refl
  right-successor-law-add-â„¤ (inr (inr (succ-â„• x))) y =
    ap succ-â„¤ (right-successor-law-add-â„¤ (inr (inr x)) y)

{- Exercise 6.12 (c) asks to prove associativity and commutativity. Note that 
   we avoid an unwieldy amount of cases by only using induction on the first 
   argument. The resulting proof term is fairly short, and we don't have to 
   present â„¤ as a certain quotient of â„• Ã— â„•. -}

abstract
  associative-add-â„¤ :
    (x y z : â„¤) â†’ Id (add-â„¤ (add-â„¤ x y) z) (add-â„¤ x (add-â„¤ y z))
  associative-add-â„¤ (inl zero-â„•) y z =
    ( ap (Î» t â†’ add-â„¤ t z) (left-predecessor-law-add-â„¤ zero-â„¤ y)) âˆ™
    ( ( left-predecessor-law-add-â„¤ y z) âˆ™
      ( inv (left-predecessor-law-add-â„¤ zero-â„¤ (add-â„¤ y z))))
  associative-add-â„¤ (inl (succ-â„• x)) y z =
    ( ap (Î» t â†’ add-â„¤ t z) (left-predecessor-law-add-â„¤ (inl x) y)) âˆ™
    ( ( left-predecessor-law-add-â„¤ (add-â„¤ (inl x) y) z) âˆ™
      ( ( ap pred-â„¤ (associative-add-â„¤ (inl x) y z)) âˆ™ 
        ( inv (left-predecessor-law-add-â„¤ (inl x) (add-â„¤ y z)))))
  associative-add-â„¤ (inr (inl star)) y z = refl
  associative-add-â„¤ (inr (inr zero-â„•)) y z =
    ( ap (Î» t â†’ add-â„¤ t z) (left-successor-law-add-â„¤ zero-â„¤ y)) âˆ™ 
    ( ( left-successor-law-add-â„¤ y z) âˆ™ 
      ( inv (left-successor-law-add-â„¤ zero-â„¤ (add-â„¤ y z))))
  associative-add-â„¤ (inr (inr (succ-â„• x))) y z =
    ( ap (Î» t â†’ add-â„¤ t z) (left-successor-law-add-â„¤ (inr (inr x)) y)) âˆ™
    ( ( left-successor-law-add-â„¤ (add-â„¤ (inr (inr x)) y) z) âˆ™
      ( ( ap succ-â„¤ (associative-add-â„¤ (inr (inr x)) y z)) âˆ™
        ( inv (left-successor-law-add-â„¤ (inr (inr x)) (add-â„¤ y z)))))

abstract
  commutative-add-â„¤ :
    (x y : â„¤) â†’ Id (add-â„¤ x y) (add-â„¤ y x)
  commutative-add-â„¤ (inl zero-â„•) y =
    ( left-predecessor-law-add-â„¤ zero-â„¤ y) âˆ™
    ( inv
      ( ( right-predecessor-law-add-â„¤ y zero-â„¤) âˆ™
        ( ap pred-â„¤ (right-unit-law-add-â„¤ y))))
  commutative-add-â„¤ (inl (succ-â„• x)) y =
    ( ap pred-â„¤ (commutative-add-â„¤ (inl x) y)) âˆ™ 
    ( inv (right-predecessor-law-add-â„¤ y (inl x)))
  commutative-add-â„¤ (inr (inl star)) y = inv (right-unit-law-add-â„¤ y)
  commutative-add-â„¤ (inr (inr zero-â„•)) y =
    inv
      ( ( right-successor-law-add-â„¤ y zero-â„¤) âˆ™
        ( ap succ-â„¤ (right-unit-law-add-â„¤ y)))
  commutative-add-â„¤ (inr (inr (succ-â„• x))) y =
    ( ap succ-â„¤ (commutative-add-â„¤ (inr (inr x)) y)) âˆ™ 
    ( inv (right-successor-law-add-â„¤ y (inr (inr x))))

{- Exercise 6.12 (d) finally asks to show the inverse laws, completing the 
   verification of the group laws. Combined with associativity and 
   commutativity we conclude that (add-â„¤ x) and (Î» x â†’ add-â„¤ x y) are 
   equivalences, for every x : â„¤ and y : â„¤, respectively. -}

abstract
  left-inverse-law-add-â„¤ :
    (x : â„¤) â†’ Id (add-â„¤ (neg-â„¤ x) x) zero-â„¤
  left-inverse-law-add-â„¤ (inl zero-â„•) = refl
  left-inverse-law-add-â„¤ (inl (succ-â„• x)) =
    ( ap succ-â„¤ (right-predecessor-law-add-â„¤ (inr (inr x)) (inl x))) âˆ™ 
    ( ( right-inverse-pred-â„¤ (add-â„¤ (inr (inr x)) (inl x))) âˆ™
      ( left-inverse-law-add-â„¤ (inl x))) 
  left-inverse-law-add-â„¤ (inr (inl star)) = refl
  left-inverse-law-add-â„¤ (inr (inr x)) =
    ( commutative-add-â„¤ (inl x) (inr (inr x))) âˆ™ 
    ( left-inverse-law-add-â„¤ (inl x))
  
  right-inverse-law-add-â„¤ :
    (x : â„¤) â†’ Id (add-â„¤ x (neg-â„¤ x)) zero-â„¤
  right-inverse-law-add-â„¤ x =
    ( commutative-add-â„¤ x (neg-â„¤ x)) âˆ™ (left-inverse-law-add-â„¤ x)

abstract
  is-equiv-add-â„¤-right :
    (x : â„¤) â†’ is-equiv (add-â„¤ x)
  is-equiv-add-â„¤-right x =
    is-equiv-has-inverse
      ( add-â„¤ (neg-â„¤ x))
      ( Î» y â†’
        ( inv (associative-add-â„¤ x (neg-â„¤ x) y)) âˆ™
        ( ap (Î» t â†’ add-â„¤ t y) (right-inverse-law-add-â„¤ x)))
      ( Î» y â†’
        ( inv (associative-add-â„¤ (neg-â„¤ x) x y)) âˆ™
        ( ap (Î» t â†’ add-â„¤ t y) (left-inverse-law-add-â„¤ x)))

abstract
  is-equiv-add-â„¤-left :
    (y : â„¤) â†’ is-equiv (Î» x â†’ add-â„¤ x y)
  is-equiv-add-â„¤-left y =
    is-equiv-htpy (add-â„¤ y)
      ( Î» x â†’ commutative-add-â„¤ x y)
      ( is-equiv-add-â„¤-right y)

-- Exercise 6.13

{- We construct the functoriality of coproducts. -}

functor-coprod :
  {l1 l2 l1' l2' : Level} {A : UU l1} {B : UU l2} {A' : UU l1'} {B' : UU l2'} â†’
  (A â†’ A') â†’ (B â†’ B') â†’ coprod A B â†’ coprod A' B'
functor-coprod f g (inl x) = inl (f x)
functor-coprod f g (inr y) = inr (g y)

htpy-functor-coprod :
  {l1 l2 l1' l2' : Level} {A : UU l1} {B : UU l2} {A' : UU l1'} {B' : UU l2'}
  {f f' : A â†’ A'} (H : f ~ f') {g g' : B â†’ B'} (K : g ~ g') â†’
  (functor-coprod f g) ~ (functor-coprod f' g')
htpy-functor-coprod H K (inl x) = ap inl (H x)
htpy-functor-coprod H K (inr y) = ap inr (K y)

id-functor-coprod :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
  (functor-coprod (id {A = A}) (id {A = B})) ~ id
id-functor-coprod A B (inl x) = refl
id-functor-coprod A B (inr x) = refl

compose-functor-coprod :
  {l1 l2 l1' l2' l1'' l2'' : Level}
  {A : UU l1} {B : UU l2} {A' : UU l1'} {B' : UU l2'}
  {A'' : UU l1''} {B'' : UU l2''}
  (f : A â†’ A') (f' : A' â†’ A'') (g : B â†’ B') (g' : B' â†’ B'') â†’
  (functor-coprod (f' âˆ˜ f) (g' âˆ˜ g)) ~
  ((functor-coprod f' g') âˆ˜ (functor-coprod f g))
compose-functor-coprod f f' g g' (inl x) = refl
compose-functor-coprod f f' g g' (inr y) = refl

abstract
  is-equiv-functor-coprod :
    {l1 l2 l1' l2' : Level} {A : UU l1} {B : UU l2} {A' : UU l1'} {B' : UU l2'}
    {f : A â†’ A'} {g : B â†’ B'} â†’
    is-equiv f â†’ is-equiv g â†’ is-equiv (functor-coprod f g)
  is-equiv-functor-coprod {A = A} {B = B} {A' = A'} {B' = B'} {f = f} {g = g}
    (pair (pair sf issec-sf) (pair rf isretr-rf))
    (pair (pair sg issec-sg) (pair rg isretr-rg)) =
    pair
      ( pair
        ( functor-coprod sf sg)
        ( ( ( htpy-inv (compose-functor-coprod sf f sg g)) âˆ™h
            ( htpy-functor-coprod issec-sf issec-sg)) âˆ™h
          ( id-functor-coprod A' B')))
      ( pair
        ( functor-coprod rf rg)
        ( ( ( htpy-inv (compose-functor-coprod f rf g rg)) âˆ™h
            ( htpy-functor-coprod isretr-rf isretr-rg)) âˆ™h
          ( id-functor-coprod A B)))
  
equiv-functor-coprod :
  {l1 l2 l1' l2' : Level} {A : UU l1} {B : UU l2} {A' : UU l1'} {B' : UU l2'} â†’
  (A â‰ƒ A') â†’ (B â‰ƒ B') â†’ ((coprod A B) â‰ƒ (coprod A' B'))
equiv-functor-coprod (pair e is-equiv-e) (pair f is-equiv-f) =
  pair
    ( functor-coprod e f)
    ( is-equiv-functor-coprod is-equiv-e is-equiv-f)

-- Extra material

abstract
  is-equiv-inv-con :
    {i : Level} {A : UU i} {x y z : A} (p : Id x y)
    (q : Id y z) (r : Id x z) â†’ is-equiv (inv-con p q r)
  is-equiv-inv-con refl q r = is-equiv-id (Id q r)

abstract
  is-equiv-con-inv :
    {i : Level} {A : UU i} {x y z : A} (p : Id x y)
    (q : Id y z) (r : Id x z) â†’ is-equiv (con-inv p q r)
  is-equiv-con-inv p refl r =
    is-equiv-comp'
      ( concat' p (inv right-unit))
      ( concat (inv right-unit) r)
      ( is-equiv-concat (inv right-unit) r)
      ( is-equiv-concat' p (inv right-unit))

htpy-inv-con :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {f g h : (x : A) â†’ B x} â†’
  (H : f ~ g) (K : g ~ h) (L : f ~ h) â†’
  (H âˆ™h K) ~ L â†’ K ~ ((htpy-inv H) âˆ™h L)
htpy-inv-con H K L M x = inv-con (H x) (K x) (L x) (M x)

htpy-con-inv :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {f g h : (x : A) â†’ B x} â†’
  (H : f ~ g) (K : g ~ h) (L : f ~ h) â†’
  (H âˆ™h K) ~ L â†’ H ~ (L âˆ™h (htpy-inv K))
htpy-con-inv H K L M x = con-inv (H x) (K x) (L x) (M x)

htpy-ap-concat :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {f g h : (x : A) â†’ B x} â†’
  (H : f ~ g) (K K' : g ~ h) â†’
  K ~ K' â†’ (H âˆ™h K) ~ (H âˆ™h K')
htpy-ap-concat {g = g} {h} H K K' L x =
  ap (concat (H x) (h x)) (L x)

htpy-ap-concat' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {f g h : (x : A) â†’ B x} â†’
  (H H' : f ~ g) (K : g ~ h) â†’
  H ~ H' â†’ (H âˆ™h K) ~ (H' âˆ™h K)
htpy-ap-concat' H H' K L x =
  ap (concat' _ (K x)) (L x)

htpy-distributive-inv-concat :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {f g h : (x : A) â†’ B x} â†’
  (H : f ~ g) (K : g ~ h) â†’
  (htpy-inv (H âˆ™h K)) ~ ((htpy-inv K) âˆ™h (htpy-inv H))
htpy-distributive-inv-concat H K x = distributive-inv-concat (H x) (K x)

htpy-ap-inv :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} {f g : (x : A) â†’ B x} â†’
  {H H' : f ~ g} â†’
  H ~ H' â†’ (htpy-inv H) ~ (htpy-inv H')
htpy-ap-inv K x = ap inv (K x)

htpy-left-whisk-htpy-inv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  {f f' : A â†’ B} (g : B â†’ C) (H : f ~ f') â†’
  (g Â·l (htpy-inv H)) ~ htpy-inv (g Â·l H)
htpy-left-whisk-htpy-inv g H x = ap-inv g (H x)

htpy-right-whisk-htpy-inv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : UU l3}
  {g g' : B â†’ C} (H : g ~ g') (f : A â†’ B) â†’
  ((htpy-inv H) Â·r f) ~ (htpy-inv (H Â·r f))
htpy-right-whisk-htpy-inv H f = htpy-refl
