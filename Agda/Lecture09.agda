{-# OPTIONS --without-K --allow-unsolved-metas #-}

module Lecture09 where

import Lecture08
open Lecture08 public

-- Section 9.1 Equivalent forms of Function Extensionality.

-- We first define the types Funext, Ind-htpy, and Weak-Funext. 

htpy-eq :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (Id f g) â†’ (f ~ g)
htpy-eq refl = htpy-refl _

FUNEXT :
  {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  (f : (x : A) â†’ B x) â†’ UU (i âŠ” j)
FUNEXT f = is-fiberwise-equiv (Î» g â†’ htpy-eq {f = f} {g = g})

ev-htpy-refl :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’
  ((g : (x : A) â†’ B x) (H : f ~ g) â†’ C g H) â†’ C f (htpy-refl f)
ev-htpy-refl f C Ï† = Ï† f (htpy-refl f)

IND-HTPY :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) â†’ UU _
IND-HTPY {l1} {l2} {l3} {A} {B} f =
  (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’ sec (ev-htpy-refl f C)

WEAK-FUNEXT :
  {i j : Level} (A : UU i) (B : A â†’ UU j) â†’ UU (i âŠ” j)
WEAK-FUNEXT A B =
  ((x : A) â†’ is-contr (B x)) â†’ is-contr ((x : A) â†’ B x)

-- Our goal is now to show that function extensionality holds if and only if the homotopy induction principle is valid, if and only if the weak function extensionality principle holds. This is Theorem 9.1.1 in the notes.

is-contr-total-htpy-Funext :
  {i j : Level} {A : UU i} {B : A â†’ UU j} â†’
  (f : (x : A) â†’ B x) â†’ FUNEXT f â†’ is-contr (Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g))
is-contr-total-htpy-Funext f funext-f =
  id-fundamental-gen' f (htpy-refl f) (Î» g â†’ htpy-eq {g = g}) funext-f

ev-pair :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : Î£ A B â†’ UU l3} â†’
  ((t : Î£ A B) â†’ C t) â†’ (x : A) (y : B x) â†’ C (dpair x y)
ev-pair f x y = f (dpair x y)

sec-ev-pair :
  {l1 l2 l3 : Level} (A : UU l1) (B : A â†’ UU l2)
  (C : Î£ A B â†’ UU l3) â†’ sec (ev-pair {A = A} {B = B} {C = C})
sec-ev-pair A B C =
  dpair (Î» f â†’ ind-Î£ f) (Î» f â†’ refl)

triangle-ev-htpy-refl :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C :  Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g) â†’ UU l3) â†’
  ev-pt (Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g)) (dpair f (htpy-refl f)) C ~
  ((ev-htpy-refl f (Î» x y â†’ C (dpair x y))) âˆ˜ (ev-pair {C = C}))
triangle-ev-htpy-refl f C Ï† = refl

IND-HTPY-FUNEXT :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (f : (x : A) â†’ B x) â†’
  FUNEXT f â†’ IND-HTPY {l3 = l3} f
IND-HTPY-FUNEXT {l3 = l3} {A = A} {B = B} f funext-f C =
  let total-C = Î» t â†’ C (pr1 t) (pr2 t) in
  section-comp
    ( ev-pt
      ( Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g))
      ( dpair f (htpy-refl f))
      ( total-C))
    ( ev-htpy-refl f C)
    ( ev-pair)
    ( triangle-ev-htpy-refl f total-C)
    ( sec-ev-pair ((x : A) â†’ B x) (Î» g â†’ f ~ g) total-C)
    ( sec-ev-pt-is-contr
      ( Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g))
      ( is-contr-total-htpy-Funext f funext-f)
      ( total-C))

FUNEXT-IND-HTPY :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} (f : (x : A) â†’ B x) â†’
  IND-HTPY {l3 = l1 âŠ” l2} f â†’ FUNEXT f
FUNEXT-IND-HTPY f ind-htpy-f =
  let eq-htpy-f = pr1 (ind-htpy-f (Î» h H â†’ Id f h)) refl in
  id-fundamental-sec f (Î» h â†’ htpy-eq {g = h}) (Î» g â†’ dpair
    ( eq-htpy-f g)
    ( pr1 (ind-htpy-f (Î» h H â†’ Id (htpy-eq (eq-htpy-f h H)) H))
      ( ap htpy-eq (pr2 (ind-htpy-f (Î» h H â†’ Id f h)) refl)) g))

WEAK-FUNEXT-FUNEXT :
  {l1 l2 : Level} â†’
  ((A : UU l1) (B : A â†’ UU l2) (f : (x : A) â†’ B x) â†’ FUNEXT f) â†’
  ((A : UU l1) (B : A â†’ UU l2) â†’ WEAK-FUNEXT A B)
WEAK-FUNEXT-FUNEXT funext A B is-contr-B =
  let pi-center = (Î» x â†’ center (is-contr-B x)) in
  dpair
    ( pi-center)
    ( Î» f â†’ inv-is-equiv (funext A B pi-center f)
      ( Î» x â†’ contraction (is-contr-B x) (f x)))

FUNEXT-WEAK-FUNEXT :
  {l1 l2 : Level} â†’
  ((A : UU l1) (B : A â†’ UU l2) â†’ WEAK-FUNEXT A B) â†’
  ((A : UU l1) (B : A â†’ UU l2) (f : (x : A) â†’ B x) â†’ FUNEXT f)
FUNEXT-WEAK-FUNEXT weak-funext A B f =
  id-fundamental-gen f (htpy-refl f)
    ( is-contr-retract-of
      ( (x : A) â†’ Î£ (B x) (Î» b â†’ Id (f x) b))
      ( dpair
        ( Î» t x â†’ dpair (pr1 t x) (pr2 t x))
        ( dpair (Î» t â†’ dpair (Î» x â†’ pr1 (t x)) (Î» x â†’ pr2 (t x)))
        ( Î» t â†’ eq-pair (dpair refl refl))))
      ( weak-funext A
        ( Î» x â†’ Î£ (B x) (Î» b â†’ Id (f x) b))
        ( Î» x â†’ is-contr-total-path (B x) (f x))))
    ( Î» g â†’ htpy-eq {g = g})

-- From now on we will be assuming that function extensionality holds

postulate funext : {i j : Level} {A : UU i} {B : A â†’ UU j} (f : (x : A) â†’ B x) â†’ FUNEXT f

eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  (f ~ g) â†’ Id f g
eq-htpy = inv-is-equiv (funext _ _)

issec-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  ((htpy-eq {f = f} {g = g}) âˆ˜ (eq-htpy {f = f} {g = g})) ~ id
issec-eq-htpy = issec-inv-is-equiv (funext _ _)

isretr-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} {f g : (x : A) â†’ B x} â†’
  ((eq-htpy {f = f} {g = g}) âˆ˜ (htpy-eq {f = f} {g = g})) ~ id
isretr-eq-htpy = isretr-inv-is-equiv (funext _ _)

is-equiv-eq-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j}
  (f g : (x : A) â†’ B x) â†’ is-equiv (eq-htpy {f = f} {g = g})
is-equiv-eq-htpy f g = is-equiv-inv-is-equiv (funext _ _)

eq-htpy-htpy-refl :
  {i j : Level} {A : UU i} {B : A â†’ UU j}
  (f : (x : A) â†’ B x) â†’ Id (eq-htpy (htpy-refl f)) refl
eq-htpy-htpy-refl f = isretr-eq-htpy refl

{-
The immediate proof of the following theorem would be

  is-contr-total-htpy-Funext f (funext f)

We give a different proof to ensure that the center of contraction is the 
expected thing: 

  dpair f (htpy-refl f)

-}

is-contr-total-htpy :
  {i j : Level} {A : UU i} {B : A â†’ UU j} (f : (x : A) â†’ B x) â†’
  is-contr (Î£ ((x : A) â†’ B x) (Î» g â†’ f ~ g))
is-contr-total-htpy f =
  dpair
    ( dpair f (htpy-refl f))
    ( Î» t â†’ concat
      ( center (is-contr-total-htpy-Funext f (funext f)))
      ( inv (contraction
        ( is-contr-total-htpy-Funext f (funext f))
        ( dpair f (htpy-refl f))))
      ( contraction (is-contr-total-htpy-Funext f (funext f)) t))

is-contr-total-htpy-nondep :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) â†’
  is-contr (Î£ (A â†’ B) (Î» g â†’ f ~ g))
is-contr-total-htpy-nondep {B = B} f =
  is-contr-total-htpy-Funext {B = Î» x â†’ B} f (funext f)

Ind-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} (f : (x : A) â†’ B x) â†’
  IND-HTPY {l3 = l3} f
Ind-htpy f = IND-HTPY-FUNEXT f (funext f)

ind-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’
  C f (htpy-refl f) â†’ (g : (x : A) â†’ B x) (H : f ~ g) â†’ C g H
ind-htpy f C = pr1 (Ind-htpy f C)

comp-htpy :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) (C : (g : (x : A) â†’ B x) â†’ (f ~ g) â†’ UU l3) â†’
  (c : C f (htpy-refl f)) â†’
  Id (ind-htpy f C c f (htpy-refl f)) c
comp-htpy f C = pr2 (Ind-htpy f C)

is-contr-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ is-contr (B x)) â†’ is-contr ((x : A) â†’ B x)
is-contr-Î  {A = A} {B = B} = WEAK-FUNEXT-FUNEXT (Î» X Y â†’ funext) A B

is-trunc-Î  :
  {l1 l2 : Level} (k : ğ•‹) {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ is-trunc k (B x)) â†’ is-trunc k ((x : A) â†’ B x)
is-trunc-Î  neg-two-ğ•‹ is-trunc-B = is-contr-Î  is-trunc-B
is-trunc-Î  (succ-ğ•‹ k) is-trunc-B f g =
  is-trunc-is-equiv k (f ~ g) htpy-eq
    ( funext f g)
    ( is-trunc-Î  k (Î» x â†’ is-trunc-B x (f x) (g x)))

is-prop-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  is-subtype B â†’ is-prop ((x : A) â†’ B x)
is-prop-Î  = is-trunc-Î  neg-one-ğ•‹

is-set-Î  :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2} â†’
  ((x : A) â†’ is-set (B x)) â†’ is-set ((x : A) â†’ (B x))
is-set-Î  = is-trunc-Î  zero-ğ•‹

is-trunc-function-type :
  {l1 l2 : Level} (k : ğ•‹) (A : UU l1) (B : UU l2) â†’
  is-trunc k B â†’ is-trunc k (A â†’ B)
is-trunc-function-type k A B is-trunc-B =
  is-trunc-Î  k {B = Î» (x : A) â†’ B} (Î» x â†’ is-trunc-B)

is-prop-function-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
  is-prop B â†’ is-prop (A â†’ B)
is-prop-function-type = is-trunc-function-type neg-one-ğ•‹

is-set-function-type :
  {l1 l2 : Level} (A : UU l1) (B : UU l2) â†’
  is-set B â†’ is-set (A â†’ B)
is-set-function-type = is-trunc-function-type zero-ğ•‹

choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  {C : (x : A) â†’ B x â†’ UU l3} â†’ ((x : A) â†’ Î£ (B x) (Î» y â†’ C x y)) â†’
  Î£ ((x : A) â†’ B x) (Î» f â†’ (x : A) â†’ C x (f x))
choice-âˆ Ï† = dpair (Î» x â†’ pr1 (Ï† x)) (Î» x â†’ pr2 (Ï† x))

is-equiv-choice-âˆ :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2}
  {C : (x : A) â†’ B x â†’ UU l3} â†’ is-equiv (choice-âˆ {A = A} {B = B} {C = C})
is-equiv-choice-âˆ {A = A} {B = B} {C = C} =
  is-equiv-has-inverse
    ( dpair
      ( Î» Ïˆ x â†’ dpair ((pr1 Ïˆ) x) ((pr2 Ïˆ) x))
      ( dpair
        ( Î» Ïˆ â†’ eq-pair (dpair
          ( eq-htpy (Î» x â†’ refl))
          ( ap
            ( Î» t â†’ tr (Î» f â†’ (x : A) â†’ C x (f x)) t (Î» x â†’ (pr2 Ïˆ) x))
            ( isretr-eq-htpy refl))))
        ( Î» Ï† â†’ eq-htpy Î» x â†’ eq-pair (dpair refl refl))))

mapping-into-Î£ :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {C : B â†’ UU l3} â†’
  (A â†’ Î£ B C) â†’ Î£ (A â†’ B) (Î» f â†’ (x : A) â†’ C (f x))
mapping-into-Î£ {B = B} = choice-âˆ {B = Î» x â†’ B}

is-equiv-mapping-into-Î£ :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  {C : B â†’ UU l3} â†’ is-equiv (mapping-into-Î£ {A = A} {C = C})
is-equiv-mapping-into-Î£ = is-equiv-choice-âˆ

-- Section 9.2 Universal properties

is-equiv-ev-pair :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : Î£ A B â†’ UU l3} â†’
  is-equiv (ev-pair {C = C})
is-equiv-ev-pair =
  dpair
    ( sec-ev-pair _ _ _)
    ( dpair ind-Î£
      ( Î» f â†’ eq-htpy
        ( ind-Î£
          {C = (Î» t â†’ Id (ind-Î£ (ev-pair f) t) (f t))}
          (Î» x y â†’ refl))))

ev-refl :
  {l1 l2 : Level} {A : UU l1} (a : A) {B : (x : A) â†’ Id a x â†’ UU l2} â†’
  ((x : A) (p : Id a x) â†’ B x p) â†’ B a refl
ev-refl a f = f a refl

is-equiv-ev-refl :
  {l1 l2 : Level} {A : UU l1} (a : A)
  {B : (x : A) â†’ Id a x â†’ UU l2} â†’ is-equiv (ev-refl a {B = B})
is-equiv-ev-refl a =
  is-equiv-has-inverse
    ( dpair (ind-Id a _)
      ( dpair
        ( Î» b â†’ refl)
        ( Î» f â†’ eq-htpy
          ( Î» x â†’ eq-htpy
            ( ind-Id a
              ( Î» x' p' â†’ Id (ind-Id a _ (f a refl) x' p') (f x' p'))
              ( refl) x)))))

-- Section 9.3 Composing with equivalences.

is-equiv-precomp-Î -is-half-adjoint-equivalence :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-half-adjoint-equivalence f â†’
  (C : B â†’ UU l3) â†’ is-equiv (Î» (s : (y : B) â†’ C y) (x : A) â†’ s (f x))
is-equiv-precomp-Î -is-half-adjoint-equivalence f
  ( dpair g (dpair issec-g (dpair isretr-g coh))) C =
  is-equiv-has-inverse
    ( dpair (Î» s y â†’ tr C (issec-g y) (s (g y)))
      ( dpair
        ( Î» s â†’ eq-htpy (Î» x â†’ concat
          ( tr C (ap f (isretr-g x)) (s (g (f x))))
          ( ap (Î» t â†’ tr C t (s (g (f x)))) (coh x))
          ( concat
            ( tr (Î» x â†’ C (f x)) (isretr-g x) (s (g (f x))))
            ( tr-precompose-fam C f (isretr-g x) (s (g (f x))))
            ( apd s (isretr-g x)))))
        ( Î» s â†’ eq-htpy Î» y â†’ apd s (issec-g y))))

is-equiv-precomp-Î -is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-equiv f â†’
  (C : B â†’ UU l3) â†’ is-equiv (Î» (s : (y : B) â†’ C y) (x : A) â†’ s (f x))
is-equiv-precomp-Î -is-equiv f is-equiv-f =
  is-equiv-precomp-Î -is-half-adjoint-equivalence f
    ( is-half-adjoint-equivalence-is-path-split f
      ( is-path-split-is-equiv f is-equiv-f))

ind-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  (C : B â†’ UU l3) (f : A â†’ B) (is-equiv-f : is-equiv f) â†’
  ((x : A) â†’ C (f x)) â†’ ((y : B) â†’ C y)
ind-is-equiv C f is-equiv-f =
  inv-is-equiv (is-equiv-precomp-Î -is-equiv f is-equiv-f C)

comp-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (C : B â†’ UU l3)
  (f : A â†’ B) (is-equiv-f : is-equiv f) (h : (x : A) â†’ C (f x)) â†’
  Id (Î» x â†’ (ind-is-equiv C f is-equiv-f h) (f x)) h
comp-is-equiv C f is-equiv-f h =
  issec-inv-is-equiv (is-equiv-precomp-Î -is-equiv f is-equiv-f C) h

htpy-comp-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  (C : B â†’ UU l3) (f : A â†’ B) (is-equiv-f : is-equiv f)
  (h : (x : A) â†’ C (f x)) â†’
  (Î» x â†’ (ind-is-equiv C f is-equiv-f h) (f x)) ~ h
htpy-comp-is-equiv C f is-equiv-f h = htpy-eq (comp-is-equiv C f is-equiv-f h)

is-equiv-precomp-is-equiv-precomp-Î  :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  ((C : B â†’ UU l3) â†’ is-equiv (Î» (s : (y : B) â†’ C y) (x : A) â†’ s (f x))) â†’
  ((C : UU l3) â†’ is-equiv (Î» (g : B â†’ C) â†’ g âˆ˜ f))
is-equiv-precomp-is-equiv-precomp-Î  f is-equiv-precomp-Î -f C =
  is-equiv-precomp-Î -f (Î» y â†’ C)

is-equiv-precomp-is-equiv :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’ is-equiv f â†’
  (C : UU l3) â†’ is-equiv (Î» (g : B â†’ C) â†’ g âˆ˜ f)
is-equiv-precomp-is-equiv f is-equiv-f =
  is-equiv-precomp-is-equiv-precomp-Î  f
    ( is-equiv-precomp-Î -is-equiv f is-equiv-f)

is-equiv-is-equiv-precomp :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  ({l3 : Level} (C : UU l3) â†’ is-equiv (Î» (g : B â†’ C) â†’ g âˆ˜ f)) â†’
  is-equiv f
is-equiv-is-equiv-precomp {A = A} {B = B} f is-equiv-precomp-f =
  let retr-f = center (is-contr-map-is-equiv (is-equiv-precomp-f A) id) in
  is-equiv-has-inverse
    ( dpair
      ( pr1 retr-f)
      ( pair
        ( htpy-eq (ap pr1 (center (is-prop-is-contr
          ( is-contr-map-is-equiv (is-equiv-precomp-f B) f)
          ( dpair (f âˆ˜ (pr1 retr-f)) (ap (Î» (g : A â†’ A) â†’ f âˆ˜ g) (pr2 retr-f)))
          ( dpair id refl)))))
        ( htpy-eq (pr2 retr-f)))) 

-- Exercises

-- Exercise 9.1

is-equiv-htpy-inv :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f g : (x : A) â†’ B x) â†’ is-equiv (htpy-inv {f = f} {g = g})
is-equiv-htpy-inv f g =
  is-equiv-has-inverse
    ( dpair htpy-inv
      ( dpair
        ( Î» H â†’ eq-htpy (Î» x â†’ inv-inv (H x)))
        ( Î» H â†’ eq-htpy (Î» x â†’ inv-inv (H x)))))

is-equiv-htpy-concat :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  {f g : (x : A) â†’ B x} (H : f ~ g) â†’
  (h : (x : A) â†’ B x) â†’ is-equiv (htpy-concat g {h = h} H)
is-equiv-htpy-concat {A = A} {B = B} {f} {g} H =
  ind-htpy f
    ( Î» g H â†’ (h : (x : A) â†’ B x) â†’ is-equiv (htpy-concat g {h = h} H))
    ( Î» h â†’ is-equiv-id (f ~ h))
    g H

htpy-concat' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) {g h : (x : A) â†’ B x} â†’
  (g ~ h) â†’ (f ~ g) â†’ (f ~ h)
htpy-concat' f K H = H âˆ™h K

inv-htpy-concat' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) {g h : (x : A) â†’ B x} â†’
  (g ~ h) â†’ (f ~ h) â†’ (f ~ g)
inv-htpy-concat' f K = htpy-concat' f (htpy-inv K)

issec-inv-htpy-concat' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) {g h : (x : A) â†’ B x}
  (K : g ~ h) â†’ ((htpy-concat' f K) âˆ˜ (inv-htpy-concat' f K)) ~ id
issec-inv-htpy-concat' f K L =
  eq-htpy (Î» x â†’ right-inv-inv-concat' (f x) (K x) (L x))

isretr-inv-htpy-concat' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) {g h : (x : A) â†’ B x}
  (K : g ~ h) â†’ ((inv-htpy-concat' f K) âˆ˜ (htpy-concat' f K)) ~ id
isretr-inv-htpy-concat' f K L =
  eq-htpy (Î» x â†’ left-inv-inv-concat' (f x) (K x) (L x))

is-equiv-htpy-concat' :
  {l1 l2 : Level} {A : UU l1} {B : A â†’ UU l2}
  (f : (x : A) â†’ B x) {g h : (x : A) â†’ B x} (K : g ~ h) â†’
  is-equiv (htpy-concat' f K)
is-equiv-htpy-concat' f K =
  is-equiv-has-inverse
    ( dpair
      ( inv-htpy-concat' f K)
      ( dpair
        ( issec-inv-htpy-concat' f K)
        ( isretr-inv-htpy-concat' f K)))

-- Exercise 9.2

is-subtype-is-contr :
  {l : Level} â†’ is-subtype {lsuc l} {A = UU l} is-contr
is-subtype-is-contr A =
  is-prop-is-contr-if-inh
    ( Î» is-contr-A â†’
      is-contr-Î£
        ( is-contr-A)
        ( Î» x â†’ is-contr-Î  (is-prop-is-contr is-contr-A x)))

is-prop-is-trunc :
  {l : Level} (k : ğ•‹) (A : UU l) â†’ is-prop (is-trunc k A)
is-prop-is-trunc neg-two-ğ•‹ = is-subtype-is-contr
is-prop-is-trunc (succ-ğ•‹ k) A =
  is-prop-Î  (Î» x â†’ is-prop-Î  (Î» y â†’ is-prop-is-trunc k (Id x y)))

-- Exercise 9.3

is-equiv-is-equiv-postcomp :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’
  ({l3 : Level} (A : UU l3) â†’ is-equiv (Î» (h : A â†’ X) â†’ f âˆ˜ h)) â†’ is-equiv f
is-equiv-is-equiv-postcomp {X = X} {Y = Y} f post-comp-equiv-f =
  let sec-f = center (is-contr-map-is-equiv (post-comp-equiv-f Y) id) in
  is-equiv-has-inverse
    ( dpair
      ( pr1 sec-f)
      ( dpair
        ( htpy-eq (pr2 sec-f))
        ( htpy-eq (ap pr1 (center (is-prop-is-contr
          ( is-contr-map-is-equiv (post-comp-equiv-f X) f)
          ( dpair ((pr1 sec-f) âˆ˜ f) (ap (Î» t â†’ t âˆ˜ f) (pr2 sec-f)))
          ( dpair id refl)))))))

is-equiv-postcomp-is-equiv :
  {l1 l2 : Level} {X : UU l1} {Y : UU l2} (f : X â†’ Y) â†’ is-equiv f â†’
  ({l3 : Level} (A : UU l3) â†’ is-equiv (Î» (h : A â†’ X) â†’ f âˆ˜ h))
is-equiv-postcomp-is-equiv {X = X} {Y = Y} f is-equiv-f A =
  is-equiv-has-inverse (dpair
    ( Î» (g : A â†’ Y) â†’ (inv-is-equiv is-equiv-f) âˆ˜ g)
    ( dpair
      ( Î» g â†’ eq-htpy (htpy-right-whisk (issec-inv-is-equiv is-equiv-f) g))
      ( Î» h â†’ eq-htpy (htpy-right-whisk (isretr-inv-is-equiv is-equiv-f) h)))) 

-- Exercise 9.4

is-contr-sec-is-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-equiv f â†’ is-contr (sec f)
is-contr-sec-is-equiv {A = A} {B = B} {f = f} is-equiv-f =
  is-contr-is-equiv'
    ( Î£ (B â†’ A) (Î» g â†’ Id (f âˆ˜ g) id))
    ( tot (Î» g â†’ htpy-eq))
    ( is-equiv-tot-is-fiberwise-equiv
      ( Î» g â†’ funext (f âˆ˜ g) id))
    ( is-contr-map-is-equiv (is-equiv-postcomp-is-equiv f is-equiv-f B) id)

is-contr-retr-is-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} {f : A â†’ B} â†’
  is-equiv f â†’ is-contr (retr f)
is-contr-retr-is-equiv {A = A} {B = B} {f = f} is-equiv-f =
  is-contr-is-equiv'
    ( Î£ (B â†’ A) (Î» h â†’ Id (h âˆ˜ f) id))
    ( tot (Î» h â†’ htpy-eq))
    ( is-equiv-tot-is-fiberwise-equiv
      ( Î» h â†’ funext (h âˆ˜ f) id))
    ( is-contr-map-is-equiv (is-equiv-precomp-is-equiv f is-equiv-f A) id)

is-contr-is-equiv-is-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2}
  {f : A â†’ B} â†’ is-equiv f â†’ is-contr (is-equiv f)
is-contr-is-equiv-is-equiv is-equiv-f =
  is-contr-prod
    ( is-contr-sec-is-equiv is-equiv-f)
    ( is-contr-retr-is-equiv is-equiv-f)

is-subtype-is-equiv :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-subtype (is-equiv {A = A} {B = B})
is-subtype-is-equiv f = is-prop-is-contr-if-inh
  ( Î» is-equiv-f â†’ is-contr-prod
    ( is-contr-sec-is-equiv is-equiv-f)
    ( is-contr-retr-is-equiv is-equiv-f))

is-emb-eqv-map :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} â†’
  is-emb (eqv-map {A = A} {B = B})
is-emb-eqv-map = is-emb-pr1-is-subtype is-subtype-is-equiv

-- Exercise 9.5

_â†”_ :
  {l1 l2 : Level} â†’ hProp l1 â†’ hProp l2 â†’ UU (l1 âŠ” l2)
P â†” Q = (pr1 P â†’ pr1 Q) Ã— (pr1 Q â†’ pr1 P)

equiv-iff :
  {l1 l2 : Level} (P : hProp l1) (Q : hProp l2) â†’
  (P â†” Q) â†’ (pr1 P â‰ƒ pr1 Q)
equiv-iff P Q t = dpair (pr1 t) (is-equiv-is-prop (pr2 P) (pr2 Q) (pr2 t))

iff-equiv :
  {l1 l2 : Level} (P : hProp l1) (Q : hProp l2) â†’
  (pr1 P â‰ƒ pr1 Q) â†’ (P â†” Q)
iff-equiv P Q equiv-PQ = dpair (pr1 equiv-PQ) (inv-is-equiv (pr2 equiv-PQ))

is-prop-iff :
  {l1 l2 : Level} (P : hProp l1) (Q : hProp l2) â†’ is-prop (P â†” Q)
is-prop-iff P Q =
  is-prop-prod
    ( is-prop-function-type (pr1 P) (pr1 Q) (pr2 Q))
    ( is-prop-function-type (pr1 Q) (pr1 P) (pr2 P))

is-prop-equiv-is-prop :
  {l1 l2 : Level} (P : hProp l1) (Q : hProp l2) â†’
  is-prop ((pr1 P) â‰ƒ (pr1 Q))
is-prop-equiv-is-prop P Q =
  is-prop-Î£
    ( is-prop-function-type (pr1 P) (pr1 Q) (pr2 Q))
    ( is-subtype-is-equiv)

is-equiv-equiv-iff :
  {l1 l2 : Level} (P : hProp l1) (Q : hProp l2) â†’ is-equiv (equiv-iff P Q)
is-equiv-equiv-iff P Q =
  is-equiv-is-prop
    ( is-prop-iff P Q)
    ( is-prop-equiv-is-prop P Q)
    ( iff-equiv P Q)

is-prop-is-contr-endomaps :
  {l : Level} (P : UU l) â†’ is-contr (P â†’ P) â†’ is-prop P
is-prop-is-contr-endomaps P H =
  is-prop-is-prop'
    ( Î» x â†’ htpy-eq (center (is-prop-is-contr H (const P P x) id)))

is-contr-endomaps-is-prop :
  {l : Level} (P : UU l) â†’ is-prop P â†’ is-contr (P â†’ P)
is-contr-endomaps-is-prop P is-prop-P =
  is-contr-is-prop-inh (is-prop-function-type P P is-prop-P) id

-- Exercise 9.6

is-prop-is-path-split :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-prop (is-path-split f)
is-prop-is-path-split f =
  is-prop-is-contr-if-inh (Î» is-path-split-f â†’
    let is-equiv-f = is-equiv-is-path-split f is-path-split-f in
    is-contr-prod
      ( is-contr-sec-is-equiv is-equiv-f)
      ( is-contr-Î 
        ( Î» x â†’ is-contr-Î 
          ( Î» y â†’ is-contr-sec-is-equiv (is-emb-is-equiv f is-equiv-f x y)))))

is-prop-is-half-adjoint-equivalence :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B) â†’
  is-prop (is-half-adjoint-equivalence f)
is-prop-is-half-adjoint-equivalence {l1} {l2} {A} {B} f =
  is-prop-is-contr-if-inh (Î» is-hae-f â†’
    let is-equiv-f = is-equiv-is-half-adjoint-equivalence f is-hae-f in
    is-contr-is-equiv'
      ( Î£ (sec f)
        ( Î» sf â†’ Î£ (((pr1 sf) âˆ˜ f) ~ id)
          ( Î» H â†’ (htpy-right-whisk (pr2 sf) f) ~ (htpy-left-whisk f H))))
      ( Î£-assoc (B â†’ A)
        ( Î» g â†’ ((f âˆ˜ g) ~ id))
        ( Î» sf â†’ Î£ (((pr1 sf) âˆ˜ f) ~ id)
          ( Î» H â†’ (htpy-right-whisk (pr2 sf) f) ~ (htpy-left-whisk f H))))
      ( is-equiv-Î£-assoc _ _ _)
      ( is-contr-Î£
        ( is-contr-sec-is-equiv is-equiv-f)
        ( Î» sf â†’ is-contr-is-equiv'
          ( (x : A) â†’
            Î£ (Id ((pr1 sf) (f x)) x) (Î» p â†’ Id ((pr2 sf) (f x)) (ap f p)))
          ( choice-âˆ)
          ( is-equiv-choice-âˆ)
          ( is-contr-Î  (Î» x â†’
             is-contr-is-equiv'
               ( fib (ap f) ((pr2 sf) (f x)))
               ( tot (Î» p â†’ inv))
               ( is-equiv-tot-is-fiberwise-equiv
                 ( Î» p â†’ is-equiv-inv (ap f p) ((pr2 sf) (f x))))
               ( is-contr-map-is-equiv
                 ( is-emb-is-equiv f is-equiv-f ((pr1 sf) (f x)) x)
                 ( (pr2 sf) (f x))))))))

-- Exercise 9.7

left-unit-law-Î£-map-gen :
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’
  is-contr A â†’ (x : A) â†’ B x â†’ Î£ A B
left-unit-law-Î£-map-gen B is-contr-A x y = dpair x y

is-equiv-left-unit-law-Î£-map-gen :
  {l1 l2 : Level} {A : UU l1} (B : A â†’ UU l2) â†’
  (is-contr-A : is-contr A) â†’
  (x : A) â†’ is-equiv (left-unit-law-Î£-map-gen B is-contr-A x)
is-equiv-left-unit-law-Î£-map-gen B is-contr-A x =
   is-equiv-comp
     ( left-unit-law-Î£-map-gen B is-contr-A x)
     ( left-unit-law-Î£-map B is-contr-A)
     ( tr B (inv (contraction is-contr-A x)))
     ( Î» y â†’ eq-pair (dpair (inv (contraction is-contr-A x)) refl))
     ( is-equiv-tr B (inv (contraction is-contr-A x)))
     ( is-equiv-left-unit-law-Î£-map B is-contr-A)

is-invertible-id-htpy-id-id :
  {l : Level} (A : UU l) â†’
  (id {A = A} ~ id {A = A}) â†’ has-inverse (id {A = A})
is-invertible-id-htpy-id-id A H = dpair id (dpair (htpy-refl id) H)

triangle-is-invertible-id-htpy-id-id :
  {l : Level} (A : UU l) â†’
  ( is-invertible-id-htpy-id-id A) ~
    ( (Î£-assoc (A â†’ A) (Î» g â†’ (id âˆ˜ g) ~ id) (Î» s â†’ ((pr1 s) âˆ˜ id) ~ id)) âˆ˜
      ( left-unit-law-Î£-map-gen
        ( Î» s â†’ ((pr1 s) âˆ˜ id) ~ id)
        ( is-contr-sec-is-equiv (is-equiv-id A)) (dpair id (htpy-refl id))))
triangle-is-invertible-id-htpy-id-id A H = refl

is-equiv-invertible-id-htpy-id-id :
  {l : Level} (A : UU l) â†’ is-equiv (is-invertible-id-htpy-id-id A)
is-equiv-invertible-id-htpy-id-id A =
   is-equiv-comp
     ( is-invertible-id-htpy-id-id A)
     ( Î£-assoc (A â†’ A) (Î» g â†’ (id âˆ˜ g) ~ id) (Î» s â†’ ((pr1 s) âˆ˜ id) ~ id))
     ( left-unit-law-Î£-map-gen
       ( Î» s â†’ ((pr1 s) âˆ˜ id) ~ id)
       ( is-contr-sec-is-equiv (is-equiv-id A))
       ( dpair id (htpy-refl id)))
     ( triangle-is-invertible-id-htpy-id-id A)
     ( is-equiv-left-unit-law-Î£-map-gen
       ( Î» s â†’ ((pr1 s) âˆ˜ id) ~ id)
       ( is-contr-sec-is-equiv (is-equiv-id A))
       ( dpair id (htpy-refl id)))
     ( is-equiv-Î£-assoc _ _ _)

-- Exercise 9.8

dependent-universal-property-empty :
  {l : Level} (P : empty â†’ UU l) â†’ is-contr ((x : empty) â†’ P x)
dependent-universal-property-empty P =
  dpair
    ( ind-empty {P = P})
    ( Î» f â†’ eq-htpy ind-empty)

universal-property-empty :
  {l : Level} (X : UU l) â†’ is-contr (empty â†’ X)
universal-property-empty X = dependent-universal-property-empty (Î» t â†’ X)

uniqueness-empty :
  {l : Level} (Y : UU l) â†’ ((l' : Level) (X : UU l') â†’
  is-contr (Y â†’ X)) â†’ is-equiv (ind-empty {P = Î» t â†’ Y})
uniqueness-empty Y H =
  is-equiv-is-equiv-precomp ind-empty
    ( Î» X â†’ is-equiv-is-contr
      ( Î» g â†’ g âˆ˜ ind-empty)
      ( H _ X)
      ( universal-property-empty X))

universal-property-empty-is-equiv-ind-empty :
  {l : Level} (X : UU l) â†’ is-equiv (ind-empty {P = Î» t â†’ X}) â†’
  ((l' : Level) (Y : UU l') â†’ is-contr (X â†’ Y))
universal-property-empty-is-equiv-ind-empty X is-equiv-ind-empty l' Y =
  is-contr-is-equiv
    ( empty â†’ Y)
    ( Î» f â†’ f âˆ˜ ind-empty)
    ( is-equiv-precomp-is-equiv ind-empty is-equiv-ind-empty Y)
    ( universal-property-empty Y)

-- Exercise 9.9

ev-inl-inr :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (P : coprod A B â†’ UU l3) â†’
  ((t : coprod A B) â†’ P t) â†’ ((x : A) â†’ P (inl x)) Ã— ((y : B) â†’ P (inr y))
ev-inl-inr P s = pair (Î» x â†’ s (inl x)) (Î» y â†’ s (inr y))

dependent-universal-property-coprod :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2}
  (P : coprod A B â†’ UU l3) â†’ is-equiv (ev-inl-inr P)
dependent-universal-property-coprod P =
  is-equiv-has-inverse
    ( dpair
      ( Î» p â†’ ind-coprod P (pr1 p) (pr2 p))
      ( dpair
        ( ind-Î£ (Î» f g â†’ eq-pair-triv _ (pair f g) (pair refl refl)))
        ( Î» s â†’ eq-htpy (ind-coprod _ (Î» x â†’ refl) Î» y â†’ refl))))

universal-property-coprod :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (X : UU l3) â†’
  is-equiv (ev-inl-inr (Î» (t : coprod A B) â†’ X))
universal-property-coprod X = dependent-universal-property-coprod (Î» t â†’ X)

uniqueness-coprod :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {Y : UU l3}
  (i : A â†’ Y) (j : B â†’ Y) â†’
  ((l : Level) (X : UU l) â†’ is-equiv (Î» (s : Y â†’ X) â†’ pair (s âˆ˜ i) (s âˆ˜ j))) â†’
  is-equiv (ind-coprod (Î» t â†’ Y) i j)
uniqueness-coprod {Y = Y} i j H =
  is-equiv-is-equiv-precomp
    ( ind-coprod _ i j)
    ( Î» X â†’ is-equiv-right-factor
      ( Î» (s : Y â†’ X) â†’ pair (s âˆ˜ i) (s âˆ˜ j))
      ( ev-inl-inr (Î» t â†’ X))
      ( Î» s â†’ s âˆ˜ (ind-coprod (Î» t â†’ Y) i j))
      ( Î» s â†’ refl)
      ( universal-property-coprod X)
      ( H _ X))

universal-property-coprod-is-equiv-ind-coprod :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} (X : UU l3)
  (i : A â†’ X) (j : B â†’ X) â†’ is-equiv (ind-coprod (Î» t â†’ X) i j) â†’
  ((l4 : Level) (Y : UU l4) â†’ is-equiv (Î» (s : X â†’ Y) â†’ pair (s âˆ˜ i) (s âˆ˜ j)))
universal-property-coprod-is-equiv-ind-coprod X i j is-equiv-ind-coprod l Y =
  is-equiv-comp
    ( Î» s â†’ pair (s âˆ˜ i) (s âˆ˜ j))
    ( ev-inl-inr (Î» t â†’ Y))
    ( Î» s â†’ s âˆ˜ (ind-coprod (Î» t â†’ X) i j))
    ( Î» s â†’ refl)
    ( is-equiv-precomp-is-equiv
      ( ind-coprod (Î» t â†’ X) i j)
      ( is-equiv-ind-coprod)
      ( Y))
    ( universal-property-coprod Y)

-- Exercise 9.10

ev-star :
  {l : Level} (P : unit â†’ UU l) â†’ ((x : unit) â†’ P x) â†’ P star
ev-star P f = f star

dependent-universal-property-unit :
  {l : Level} (P : unit â†’ UU l) â†’ is-equiv (ev-star P)
dependent-universal-property-unit P =
  is-equiv-has-inverse
    ( dpair
      ( ind-unit)
      ( dpair
        ( Î» p â†’ refl)
        ( Î» f â†’ eq-htpy (ind-unit refl))))
  
universal-property-unit :
  {l : Level} (Y : UU l) â†’ is-equiv (ev-star (Î» t â†’ Y))
universal-property-unit Y = dependent-universal-property-unit (Î» t â†’ Y)

is-equiv-ind-unit-universal-property-unit :
  {l1 : Level} (X : UU l1) (x : X) â†’
  ((l2 : Level) (Y : UU l2) â†’ is-equiv (Î» (f : X â†’ Y) â†’ f x)) â†’
  is-equiv (ind-unit {P = Î» t â†’ X} x)
is-equiv-ind-unit-universal-property-unit X x H =
   is-equiv-is-equiv-precomp
     ( ind-unit x)
     ( Î» Y â†’ is-equiv-right-factor
       ( Î» f â†’ f x)
       ( ev-star (Î» t â†’ Y))
       ( Î» f â†’ f âˆ˜ (ind-unit x))
       ( Î» f â†’ refl)
       ( universal-property-unit Y)
       ( H _ Y))

universal-property-unit-is-equiv-ind-unit :
  {l1 : Level} (X : UU l1) (x : X) â†’
  is-equiv (ind-unit {P = Î» t â†’ X} x) â†’
  ((l2 : Level) (Y : UU l2) â†’ is-equiv (Î» (f : X â†’ Y) â†’ f x))
universal-property-unit-is-equiv-ind-unit X x is-equiv-ind-unit l2 Y =
  is-equiv-comp
    ( Î» f â†’ f x)
    ( ev-star (Î» t â†’ Y))
    ( Î» f â†’ f âˆ˜ (ind-unit x))
    ( Î» f â†’ refl)
    ( is-equiv-precomp-is-equiv (ind-unit x) is-equiv-ind-unit Y)
    ( universal-property-unit Y)

-- Exercise 9.11

tr-issec-eq-htpy :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (g g' : B â†’ A) (H : g ~ g') (G : (f âˆ˜ g) ~ id) â†’
  (tr (Î» (h : B â†’ A) â†’ (f âˆ˜ h) ~ id) (eq-htpy H) G) ~ ((htpy-inv (f Â·l H)) âˆ™h G)
tr-issec-eq-htpy {A = A} {B = B} f g =
  let P = Î» (h : B â†’ A) â†’ (f âˆ˜ h) ~ id in
  ind-htpy g
    ( Î» g' H â†’ (G : (f âˆ˜ g) ~ id) â†’
      ( tr P (eq-htpy H) G) ~ ((htpy-inv (f Â·l H)) âˆ™h G))
    ( Î» G â†’ htpy-eq (ap (Î» t â†’ tr P t G) (eq-htpy-htpy-refl g))) 

sec-left-factor-retract-of-sec-composition :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  sec h â†’ (sec g) retract-of (sec f)
sec-left-factor-retract-of-sec-composition {X = X} f g h H sec-h =
  dpair
    ( section-comp' f g h H sec-h)
    ( dpair
      ( section-comp f g h H sec-h)
      ( Î» sec-g â†’
        let K = htpy-right-whisk (pr2 sec-h) (pr1 sec-g) in
        eq-pair
          ( dpair
          ( eq-htpy K)
          ( eq-htpy
            ( ( tr-issec-eq-htpy g
                ( h âˆ˜ ((pr1 sec-h) âˆ˜ (pr1 sec-g)))
                ( pr1 sec-g)
                ( K)
                ( pr2
                  ( section-comp f g h H sec-h
                    ( section-comp' f g h H sec-h sec-g)))) âˆ™h
              ( ( htpy-ap-concat
                  ( htpy-inv (g Â·l ((pr2 sec-h) Â·r (pr1 sec-g)))) _ _
                  ( ( htpy-assoc
                      ( htpy-inv (H Â·r ((pr1 sec-h) âˆ˜ (pr1 sec-g))))
                      ( H Â·r ((pr1 sec-h) âˆ˜ (pr1 sec-g)))
                      ( _)) âˆ™h
                    ( htpy-ap-concat' _ _
                      ( ( g Â·l ((pr2 sec-h) Â·r (pr1 sec-g))) âˆ™h
                        ( pr2 sec-g))
                      ( htpy-left-inv (H Â·r ((pr1 sec-h) âˆ˜ (pr1 sec-g))))))) âˆ™h
                ( ( htpy-assoc
                    ( htpy-inv (g Â·l ((pr2 sec-h) Â·r (pr1 sec-g))))
                    ( g Â·l ((pr2 sec-h) Â·r (pr1 sec-g)))
                    ( pr2 sec-g)) âˆ™h
                  ( htpy-ap-concat'
                    ( ( htpy-inv (g Â·l ((pr2 sec-h) Â·r (pr1 sec-g)))) âˆ™h
                      ( g Â·l ((pr2 sec-h) Â·r (pr1 sec-g))))
                    ( htpy-refl (g âˆ˜ (pr1 sec-g)))
                    ( pr2 sec-g)
                    ( htpy-left-inv
                      ( g Â·l ((pr2 sec-h) Â·r (pr1 sec-g))))))))))))

tr-isretr-eq-htpy :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (f : A â†’ B)
  (h h' : B â†’ A) (H : h ~ h') (is-retr-h : (h âˆ˜ f) ~ id) â†’
  (tr (Î» (k : B â†’ A) â†’ (k âˆ˜ f) ~ id) (eq-htpy H) is-retr-h) ~
  ((htpy-inv (H Â·r f)) âˆ™h is-retr-h)
tr-isretr-eq-htpy {A = A} {B} f h h' H is-retr-h =
  let P = Î» (k : B â†’ A) â†’ (k âˆ˜ f) ~ id in
  ind-htpy h
    ( Î» h' H â†’
      ( K : (h âˆ˜ f) ~ id) â†’ (tr P (eq-htpy H) K) ~ ((htpy-inv (H Â·r f)) âˆ™h K))
    ( Î» K â†’ htpy-eq (ap (Î» t â†’ tr P t K) (eq-htpy-htpy-refl h)))
    h' H is-retr-h
  
sec-right-factor-retract-of-sec-left-factor :
  {l1 l2 l3 : Level} {A : UU l1} {B : UU l2} {X : UU l3}
  (f : A â†’ X) (g : B â†’ X) (h : A â†’ B) (H : f ~ (g âˆ˜ h)) â†’
  retr g â†’ (retr h) retract-of (retr f)
sec-right-factor-retract-of-sec-left-factor f g h H retr-g =
  dpair
    ( retraction-comp' f g h H retr-g)
    ( dpair
      ( retraction-comp f g h H retr-g)
      ( Î» retr-h â†’
        eq-pair
        ( dpair 
          ( eq-htpy ((pr1 retr-h) Â·l (pr2 retr-g)))
          ( eq-htpy
            ( ( tr-isretr-eq-htpy h _ _
                ( (pr1 retr-h) Â·l (pr2 retr-g))
                ( pr2
                  ( retraction-comp f g h H retr-g
                  ( retraction-comp' f g h H retr-g retr-h)))) âˆ™h
              ( ( htpy-ap-concat
                  ( htpy-inv (((pr1 retr-h) Â·l (pr2 retr-g)) Â·r h))
                  ( _)
                  ( _)
                  ( ( htpy-assoc
                      ( htpy-inv (((pr1 retr-h) âˆ˜ (pr1 retr-g)) Â·l H))
                      ( ((pr1 retr-h) âˆ˜ (pr1 retr-g)) Â·l H)
                      ( _)) âˆ™h
                     ( htpy-ap-concat' _ _
                       ( ((pr1 retr-h) Â·l ((pr2 retr-g) Â·r h)) âˆ™h (pr2 retr-h))
                       ( htpy-left-inv
                         ( ((pr1 retr-h) âˆ˜ (pr1 retr-g)) Â·l H))))) âˆ™h
                ( ( htpy-assoc
                    ( htpy-inv ((pr1 retr-h) Â·l ((pr2 retr-g) Â·r h)))
                    ( (pr1 retr-h) Â·l ((pr2 retr-g) Â·r h))
                    ( pr2 retr-h)) âˆ™h
                  ( htpy-ap-concat' _ _ (pr2 retr-h)
                    ( htpy-left-inv
                      ( (pr1 retr-h) Â·l ((pr2 retr-g) Â·r h)))))))))))

-- Exercise 9.12

postcomp-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (e : (i : I) â†’ A i â†’ B i) â†’
  ((i : I) â†’ A i) â†’ ((i : I) â†’ B i)
postcomp-Î  e f i = e i (f i)

is-equiv-postcomp-Î  :
  {l1 l2 l3 : Level} {I : UU l1} {A : I â†’ UU l2} {B : I â†’ UU l3}
  (e : (i : I) â†’ A i â†’ B i) (is-equiv-e : is-fiberwise-equiv e) â†’
  is-equiv (postcomp-Î  e)
is-equiv-postcomp-Î  e is-equiv-e =
  is-equiv-has-inverse
    ( dpair
      ( Î» g i â†’ inv-is-equiv (is-equiv-e i) (g i))
      ( dpair
        ( Î» g â†’ eq-htpy (Î» i â†’ issec-inv-is-equiv (is-equiv-e i) (g i)))
         Î» f â†’ eq-htpy (Î» i â†’ isretr-inv-is-equiv (is-equiv-e i) (f i))))

-- Exercise 9.13

hom-slice :
  {l1 l2 l3 : Level} (X : UU l1) {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ UU (l1 âŠ” (l2 âŠ” l3))
hom-slice X {A} {B} f g = Î£ (A â†’ B) (Î» h â†’ f ~ (g âˆ˜ h))
  
fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  hom-slice X f g â†’ (x : X) â†’ (fib f x) â†’ (fib g x)
fiberwise-hom-hom-slice f g (dpair h H) = fib-triangle f g h H

hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((x : X) â†’ (fib f x) â†’ (fib g x)) â†’ hom-slice X f g
hom-slice-fiberwise-hom f g Î± =
  dpair
    ( Î» a â†’ pr1 (Î± (f a) (dpair a refl)))
    ( Î» a â†’ inv (pr2 (Î± (f a) (dpair a refl))))

issec-hom-slice-fiberwise-hom-eq-htpy :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) (Î± : (x : X) â†’ (fib f x) â†’ (fib g x)) (x : X) â†’
  (fiberwise-hom-hom-slice f g (hom-slice-fiberwise-hom f g Î±) x) ~ (Î± x)
issec-hom-slice-fiberwise-hom-eq-htpy f g Î± .(f a) (dpair a refl) =
  eq-pair (dpair refl (inv-inv (pr2 (Î± (f a) (dpair a refl)))))

issec-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((fiberwise-hom-hom-slice f g) âˆ˜ (hom-slice-fiberwise-hom f g)) ~ id
issec-hom-slice-fiberwise-hom f g Î± =
  eq-htpy (Î» x â†’ eq-htpy (issec-hom-slice-fiberwise-hom-eq-htpy f g Î± x))

isretr-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  ((hom-slice-fiberwise-hom f g) âˆ˜ (fiberwise-hom-hom-slice f g)) ~ id
isretr-hom-slice-fiberwise-hom f g (dpair h H) =
  eq-pair
    ( dpair
      ( refl)
      ( eq-htpy (Î» a â†’ (inv-inv (H a)))))

is-equiv-fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (fiberwise-hom-hom-slice f g)
is-equiv-fiberwise-hom-hom-slice f g =
  is-equiv-has-inverse
    ( dpair
      ( hom-slice-fiberwise-hom f g)
      ( dpair
        ( issec-hom-slice-fiberwise-hom f g)
        ( isretr-hom-slice-fiberwise-hom f g)))

is-equiv-hom-slice-fiberwise-hom :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (hom-slice-fiberwise-hom f g)
is-equiv-hom-slice-fiberwise-hom f g =
  is-equiv-has-inverse
    ( dpair
      ( fiberwise-hom-hom-slice f g)
      ( dpair
        ( isretr-hom-slice-fiberwise-hom f g)
        ( issec-hom-slice-fiberwise-hom f g)))

equiv-slice :
  {l1 l2 l3 : Level} (X : UU l1) {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’ UU (l1 âŠ” (l2 âŠ” l3))
equiv-slice X {A} {B} f g = Î£ (A â‰ƒ B) (Î» e â†’ f ~ (g âˆ˜ (eqv-map e)))

hom-slice-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice X f g â†’ hom-slice X f g
hom-slice-equiv-slice f g (dpair (dpair h is-equiv-h) H) = dpair h H

{- We first prove two closely related generic lemmas that establishes 
   equivalences of subtypes -}

is-equiv-subtype-is-equiv :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {P : A â†’ UU l3} {Q : B â†’ UU l4}
  (is-subtype-P : is-subtype P) (is-subtype-Q : is-subtype Q)
  (f : A â†’ B) (g : (x : A) â†’ P x â†’ Q (f x)) â†’
  is-equiv f â†’ ((x : A) â†’ (Q (f x)) â†’ P x) â†’ is-equiv (toto Q f g)
is-equiv-subtype-is-equiv {Q = Q} is-subtype-P is-subtype-Q f g is-equiv-f h =
  is-equiv-toto-is-fiberwise-equiv-is-equiv-base-map Q f g is-equiv-f
    ( Î» x â†’ is-equiv-is-prop (is-subtype-P x) (is-subtype-Q (f x)) (h x))

is-equiv-subtype-is-equiv' :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {P : A â†’ UU l3} {Q : B â†’ UU l4}
  (is-subtype-P : is-subtype P) (is-subtype-Q : is-subtype Q)
  (f : A â†’ B) (g : (x : A) â†’ P x â†’ Q (f x)) â†’
  (is-equiv-f : is-equiv f) â†’
  ((y : B) â†’ (Q y) â†’ P (inv-is-equiv is-equiv-f y)) â†’
  is-equiv (toto Q f g)
is-equiv-subtype-is-equiv' {P = P} {Q}
  is-subtype-P is-subtype-Q f g is-equiv-f h =
  is-equiv-toto-is-fiberwise-equiv-is-equiv-base-map Q f g is-equiv-f
    ( Î» x â†’ is-equiv-is-prop (is-subtype-P x) (is-subtype-Q (f x))
      ( (tr P (isretr-inv-is-equiv is-equiv-f x)) âˆ˜ (h (f x))))

is-fiberwise-equiv-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X)
  (t : hom-slice X f g) â†’ is-equiv (pr1 t) â†’
  is-fiberwise-equiv (fiberwise-hom-hom-slice f g t)
is-fiberwise-equiv-fiberwise-equiv-equiv-slice f g (dpair h H) =
  is-fiberwise-equiv-is-equiv-triangle f g h H

Î±-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  Î£ (hom-slice X f g) (Î» hH â†’ is-equiv (pr1 hH)) â†’
  Î£ ((x : X) â†’ (fib f x) â†’ (fib g x)) is-fiberwise-equiv
Î±-fiberwise-equiv-equiv-slice f g =
  toto
    ( is-fiberwise-equiv)
    ( fiberwise-hom-hom-slice f g)
    ( is-fiberwise-equiv-fiberwise-equiv-equiv-slice f g)

Î²-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  Î£ (A â†’ B) (Î» h â†’ (f ~ (g âˆ˜ h)) Ã— is-equiv h) â†’
  Î£ (hom-slice X f g) (Î» hH â†’ is-equiv (pr1 hH))
Î²-fiberwise-equiv-equiv-slice {X = X} {A} {B} f g =
  inv-is-equiv
    ( is-equiv-Î£-assoc
      ( A â†’ B)
      ( Î» h â†’ f ~ (g âˆ˜ h))
      ( Î» t â†’ is-equiv (pr1 t)))

Î³-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  Î£ (A â†’ B) (Î» h â†’ (is-equiv h) Ã— (f ~ (g âˆ˜ h))) â†’
  Î£ (A â†’ B) (Î» h â†’ (f ~ (g âˆ˜ h)) Ã— is-equiv h)
Î³-fiberwise-equiv-equiv-slice f g =
  tot (Î» h â†’ swap-prod (is-equiv h) (f ~ (g âˆ˜ h)))

Î´-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice X f g â†’ Î£ (A â†’ B) (Î» h â†’ (is-equiv h) Ã— (f ~ (g âˆ˜ h)))
Î´-fiberwise-equiv-equiv-slice {X = X} {A} {B} f g =
  Î£-assoc (A â†’ B) is-equiv (Î» t â†’ f ~ (g âˆ˜ (pr1 t)))

fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  equiv-slice X f g â†’ Î£ ((x : X) â†’ (fib f x) â†’ (fib g x)) is-fiberwise-equiv
fiberwise-equiv-equiv-slice {X = X} {A} {B} f g =
  ( ( ( Î±-fiberwise-equiv-equiv-slice f g) âˆ˜
      ( Î²-fiberwise-equiv-equiv-slice f g)) âˆ˜
    ( Î³-fiberwise-equiv-equiv-slice f g)) âˆ˜
  ( Î´-fiberwise-equiv-equiv-slice f g)

is-equiv-hom-slice-is-fiberwise-equiv-fiberwise-hom-hom-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  (t : hom-slice X f g) â†’
  ((x : X) â†’ is-equiv (fiberwise-hom-hom-slice f g t x)) â†’
  is-equiv (pr1 t)
is-equiv-hom-slice-is-fiberwise-equiv-fiberwise-hom-hom-slice
  f g (dpair h H) =
  is-equiv-triangle-is-fiberwise-equiv f g h H

is-equiv-fiberwise-equiv-equiv-slice :
  {l1 l2 l3 : Level} {X : UU l1} {A : UU l2} {B : UU l3}
  (f : A â†’ X) (g : B â†’ X) â†’
  is-equiv (fiberwise-equiv-equiv-slice f g)
is-equiv-fiberwise-equiv-equiv-slice {X = X} {A} {B} f g =
  is-equiv-comp
    ( fiberwise-equiv-equiv-slice f g)
    ( ( ( Î±-fiberwise-equiv-equiv-slice f g) âˆ˜
        ( Î²-fiberwise-equiv-equiv-slice f g)) âˆ˜
      ( Î³-fiberwise-equiv-equiv-slice f g))
    ( Î´-fiberwise-equiv-equiv-slice f g)
    ( htpy-refl _)
    ( is-equiv-Î£-assoc _ _ _)
    ( is-equiv-comp
      ( ( ( Î±-fiberwise-equiv-equiv-slice f g) âˆ˜
          ( Î²-fiberwise-equiv-equiv-slice f g)) âˆ˜
        ( Î³-fiberwise-equiv-equiv-slice f g))
      ( ( Î±-fiberwise-equiv-equiv-slice f g) âˆ˜
        ( Î²-fiberwise-equiv-equiv-slice f g))
      ( Î³-fiberwise-equiv-equiv-slice f g)
      ( htpy-refl _)
      ( is-equiv-tot-is-fiberwise-equiv (Î» h â†’ is-equiv-swap-prod _ _))
      ( is-equiv-comp
        ( ( Î±-fiberwise-equiv-equiv-slice f g) âˆ˜
          ( Î²-fiberwise-equiv-equiv-slice f g))
        ( Î±-fiberwise-equiv-equiv-slice f g)
        ( Î²-fiberwise-equiv-equiv-slice f g)
        ( htpy-refl _)
        ( is-equiv-inv-is-equiv (is-equiv-Î£-assoc _ _ _))
        ( is-equiv-subtype-is-equiv
          ( Î» t â†’ is-subtype-is-equiv (pr1 t))
          ( Î» Î± â†’ is-prop-Î  (Î» x â†’ is-subtype-is-equiv (Î± x)))
          ( fiberwise-hom-hom-slice f g)
          ( is-fiberwise-equiv-fiberwise-equiv-equiv-slice f g)
          ( is-equiv-fiberwise-hom-hom-slice f g)
          ( is-equiv-hom-slice-is-fiberwise-equiv-fiberwise-hom-hom-slice
            f g))))

-- Exercise 9.14

hom-over-morphism :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’ UU (l1 âŠ” (l2 âŠ” l4))
hom-over-morphism i f g = hom-slice _ (i âˆ˜ f) g

fiberwise-hom-hom-over-morphism :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  hom-over-morphism i f g â†’ (x : X) â†’ (fib f x) â†’ (fib g (i x))
fiberwise-hom-hom-over-morphism i f g (dpair h H) .(f a) (dpair a refl) =
  dpair (h a) (inv (H a))

hom-over-morphism-fiberwise-hom :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  ((x : X) â†’ (fib f x) â†’ (fib g (i x))) â†’ hom-over-morphism i f g
hom-over-morphism-fiberwise-hom i f g Î± =
  dpair
    ( Î» a â†’ pr1 (Î± (f a) (dpair a refl)))
    ( Î» a â†’ inv (pr2 (Î± (f a) (dpair a refl))))

issec-hom-over-morphism-fiberwise-hom-eq-htpy :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  (Î± : (x : X) â†’ (fib f x) â†’ (fib g (i x))) (x : X) â†’
  ( fiberwise-hom-hom-over-morphism i f g
    ( hom-over-morphism-fiberwise-hom i f g Î±) x) ~ (Î± x)
issec-hom-over-morphism-fiberwise-hom-eq-htpy i f g Î± .(f a) (dpair a refl) =
  eq-pair (dpair refl (inv-inv (pr2 (Î± (f a) (dpair a refl)))))

issec-hom-over-morphism-fiberwise-hom :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  ( ( fiberwise-hom-hom-over-morphism i f g) âˆ˜
    ( hom-over-morphism-fiberwise-hom i f g)) ~ id
issec-hom-over-morphism-fiberwise-hom i f g Î± =
  eq-htpy
    ( Î» x â†’
      ( eq-htpy
        ( issec-hom-over-morphism-fiberwise-hom-eq-htpy i f g Î± x)))

isretr-hom-over-morphism-fiberwise-hom :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  ( ( hom-over-morphism-fiberwise-hom i f g) âˆ˜
    ( fiberwise-hom-hom-over-morphism i f g)) ~ id
isretr-hom-over-morphism-fiberwise-hom i f g (dpair h H) =
  eq-pair (dpair refl (eq-htpy (Î» a â†’ (inv-inv (H a)))))

is-equiv-fiberwise-hom-hom-over-morphism :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  is-equiv (fiberwise-hom-hom-over-morphism i f g)
is-equiv-fiberwise-hom-hom-over-morphism i f g =
  is-equiv-has-inverse
    ( dpair
      ( hom-over-morphism-fiberwise-hom i f g)
      ( dpair
        ( issec-hom-over-morphism-fiberwise-hom i f g)
        ( isretr-hom-over-morphism-fiberwise-hom i f g)))

is-equiv-hom-over-morphism-fiberwise-hom :
  {l1 l2 l3 l4 : Level} {A : UU l1} {B : UU l2} {X : UU l3} {Y : UU l4}
  (i : X â†’ Y) (f : A â†’ X) (g : B â†’ Y) â†’
  is-equiv (hom-over-morphism-fiberwise-hom i f g)
is-equiv-hom-over-morphism-fiberwise-hom i f g =
  is-equiv-has-inverse
    ( dpair
      ( fiberwise-hom-hom-over-morphism i f g)
      ( dpair
        ( isretr-hom-over-morphism-fiberwise-hom i f g)
        ( issec-hom-over-morphism-fiberwise-hom i f g)))
