{-# OPTIONS --without-K --exact-split #-}

module 18-circle where

import 17-groups
open 17-groups public

{- Section 11.1 The induction principle of the circle -}

free-loops :
  { l1 : Level} (X : UU l1) â†’ UU l1
free-loops X = Î£ X (Î» x â†’ Id x x)

base-free-loop :
  { l1 : Level} {X : UU l1} â†’ free-loops X â†’ X
base-free-loop = pr1

loop-free-loop :
  { l1 : Level} {X : UU l1} (l : free-loops X) â†’
  Id (base-free-loop l) (base-free-loop l)
loop-free-loop = pr2

-- Now we characterize the identity types of free loops

Eq-free-loops :
  { l1 : Level} {X : UU l1} (l l' : free-loops X) â†’ UU l1
Eq-free-loops (pair x l) l' =
  Î£ (Id x (pr1 l')) (Î» p â†’ Id (l âˆ™ p) (p âˆ™ (pr2 l')))

reflexive-Eq-free-loops :
  { l1 : Level} {X : UU l1} (l : free-loops X) â†’ Eq-free-loops l l
reflexive-Eq-free-loops (pair x l) = pair refl right-unit

Eq-free-loops-eq :
  { l1 : Level} {X : UU l1} (l l' : free-loops X) â†’
  Id l l' â†’ Eq-free-loops l l'
Eq-free-loops-eq l .l refl = reflexive-Eq-free-loops l

abstract
  is-contr-total-Eq-free-loops :
    { l1 : Level} {X : UU l1} (l : free-loops X) â†’
    is-contr (Î£ (free-loops X) (Eq-free-loops l))
  is-contr-total-Eq-free-loops (pair x l) =
    is-contr-total-Eq-structure
      ( Î» x l' p â†’ Id (l âˆ™ p) (p âˆ™ l'))
      ( is-contr-total-path x)
      ( pair x refl)
      ( is-contr-is-equiv'
        ( Î£ (Id x x) (Î» l' â†’ Id l l'))
        ( tot (Î» l' Î± â†’ right-unit âˆ™ Î±))
        ( is-equiv-tot-is-fiberwise-equiv
          ( Î» l' â†’ is-equiv-concat right-unit l'))
        ( is-contr-total-path l))

abstract
  is-equiv-Eq-free-loops-eq :
    { l1 : Level} {X : UU l1} (l l' : free-loops X) â†’
    is-equiv (Eq-free-loops-eq l l')
  is-equiv-Eq-free-loops-eq l =
    fundamental-theorem-id l
      ( reflexive-Eq-free-loops l)
      ( is-contr-total-Eq-free-loops l)
      ( Eq-free-loops-eq l) 

{- We introduce dependent free loops, which are used in the induction principle
   of the circle. -}
   
dependent-free-loops :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2) â†’ UU l2
dependent-free-loops l P =
  Î£ ( P (base-free-loop l))
    ( Î» pâ‚€ â†’ Id (tr P (loop-free-loop l) pâ‚€) pâ‚€)

Eq-dependent-free-loops :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2) â†’
  ( p p' : dependent-free-loops l P) â†’ UU l2
Eq-dependent-free-loops (pair x l) P (pair y p) p' =
  Î£ ( Id y (pr1 p'))
    ( Î» q â†’ Id (p âˆ™ q) ((ap (tr P l) q) âˆ™ (pr2 p')))

reflexive-Eq-dependent-free-loops :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2) â†’
  ( p : dependent-free-loops l P) â†’ Eq-dependent-free-loops l P p p
reflexive-Eq-dependent-free-loops (pair x l) P (pair y p) =
  pair refl right-unit

Eq-dependent-free-loops-eq :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2) â†’
  ( p p' : dependent-free-loops l P) â†’
  Id p p' â†’ Eq-dependent-free-loops l P p p'
Eq-dependent-free-loops-eq l P p .p refl =
  reflexive-Eq-dependent-free-loops l P p

abstract
  is-contr-total-Eq-dependent-free-loops :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2) â†’
    ( p : dependent-free-loops l P) â†’
    is-contr (Î£ (dependent-free-loops l P) (Eq-dependent-free-loops l P p))
  is-contr-total-Eq-dependent-free-loops (pair x l) P (pair y p) =
    is-contr-total-Eq-structure
      ( Î» y' p' q â†’ Id (p âˆ™ q) ((ap (tr P l) q) âˆ™ p'))
      ( is-contr-total-path y)
      ( pair y refl)
      ( is-contr-is-equiv'
        ( Î£ (Id (tr P l y) y) (Î» p' â†’ Id p p'))
        ( tot (Î» p' Î± â†’ right-unit âˆ™ Î±))
        ( is-equiv-tot-is-fiberwise-equiv
          ( Î» p' â†’ is-equiv-concat right-unit p'))
        ( is-contr-total-path p))

abstract
  is-equiv-Eq-dependent-free-loops-eq :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2)
    ( p p' : dependent-free-loops l P) â†’
    is-equiv (Eq-dependent-free-loops-eq l P p p')
  is-equiv-Eq-dependent-free-loops-eq l P p =
    fundamental-theorem-id p
      ( reflexive-Eq-dependent-free-loops l P p)
      ( is-contr-total-Eq-dependent-free-loops l P p)
      ( Eq-dependent-free-loops-eq l P p)

eq-Eq-dependent-free-loops :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2)
  ( p p' : dependent-free-loops l P) â†’
  Eq-dependent-free-loops l P p p' â†’ Id p p'
eq-Eq-dependent-free-loops l P p p' =
  inv-is-equiv (is-equiv-Eq-dependent-free-loops-eq l P p p')

{- We now define the induction principle of the circle. -}

ev-free-loop' :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (P : X â†’ UU l2) â†’
  ( (x : X) â†’ P x) â†’ dependent-free-loops l P
ev-free-loop' (pair xâ‚€ p) P f = pair (f xâ‚€) (apd f p)

induction-principle-circle :
  { l1 : Level} (l2 : Level) {X : UU l1} (l : free-loops X) â†’
  UU ((lsuc l2) âŠ” l1)
induction-principle-circle l2 {X} l =
  (P : X â†’ UU l2) â†’ sec (ev-free-loop' l P)

{- Section 11.2 The universal property of the circle -}

{- We first state the universal property of the circle -}

ev-free-loop :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (Y : UU l2) â†’
  ( X â†’ Y) â†’ free-loops Y
ev-free-loop l Y f = pair (f (pr1 l)) (ap f (pr2 l))

universal-property-circle :
  { l1 : Level} (l2 : Level) {X : UU l1} (l : free-loops X) â†’ UU _
universal-property-circle l2 l =
  ( Y : UU l2) â†’ is-equiv (ev-free-loop l Y)

{- A fairly straightforward proof of the universal property of the circle
   factors through the dependent universal property of the circle. -}

dependent-universal-property-circle :
  { l1 : Level} (l2 : Level) {X : UU l1} (l : free-loops X) â†’
  UU ((lsuc l2) âŠ” l1)
dependent-universal-property-circle l2 {X} l =
  ( P : X â†’ UU l2) â†’ is-equiv (ev-free-loop' l P)

{- We first prove that the dependent universal property of the circle follows
   from the induction principle of the circle. To show this, we have to show
   that the section of ev-free-loop' is also a retraction. This construction
   is also by the induction principle of the circle, but it requires (a minimal
   amount of) preparations. -}

Eq-subst :
  { l1 l2 : Level} {X : UU l1} {P : X â†’ UU l2} (f g : (x : X) â†’ P x) â†’
  X â†’ UU _
Eq-subst f g x = Id (f x) (g x)

tr-Eq-subst :
  { l1 l2 : Level} {X : UU l1} {P : X â†’ UU l2} (f g : (x : X) â†’ P x)
  { x y : X} (p : Id x y) (q : Id (f x) (g x)) (r : Id (f y) (g y))â†’
  ( Id ((apd f p) âˆ™ r) ((ap (tr P p) q) âˆ™ (apd g p))) â†’
  ( Id (tr (Eq-subst f g) p q) r)
tr-Eq-subst f g refl q .((ap id q) âˆ™ refl) refl =
  inv (right-unit âˆ™ (ap-id q))

dependent-free-loops-htpy :
  {l1 l2 : Level} {X : UU l1} {l : free-loops X} {P : X â†’ UU l2}
  {f g : (x : X) â†’ P x} â†’
  ( Eq-dependent-free-loops l P (ev-free-loop' l P f) (ev-free-loop' l P g)) â†’
  ( dependent-free-loops l (Î» x â†’ Id (f x) (g x)))
dependent-free-loops-htpy {l = (pair x l)} (pair p q) =
  pair p (tr-Eq-subst _ _ l p p q)

isretr-ind-circle :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
  ( ind-circle : induction-principle-circle l2 l) (P : X â†’ UU l2) â†’
  ( (pr1 (ind-circle P)) âˆ˜ (ev-free-loop' l P)) ~ id
isretr-ind-circle l ind-circle P f =
  eq-htpy
    ( pr1
      ( ind-circle
        ( Î» t â†’ Id (pr1 (ind-circle P) (ev-free-loop' l P f) t) (f t)))
      ( dependent-free-loops-htpy
        ( Eq-dependent-free-loops-eq l P _ _
          ( pr2 (ind-circle P) (ev-free-loop' l P f)))))

abstract
  dependent-universal-property-induction-principle-circle :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
    induction-principle-circle l2 l â†’ dependent-universal-property-circle l2 l
  dependent-universal-property-induction-principle-circle l ind-circle P =
    is-equiv-has-inverse
      ( pr1 (ind-circle P))
      ( pr2 (ind-circle P))
      ( isretr-ind-circle l ind-circle P)

{- We use the dependent universal property to derive a uniqeness property of
   dependent functions on the circle. -}

dependent-uniqueness-circle :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
  dependent-universal-property-circle l2 l â†’
  { P : X â†’ UU l2} (k : dependent-free-loops l P) â†’
  is-contr
    ( Î£ ( (x : X) â†’ P x)
        ( Î» h â†’ Eq-dependent-free-loops l P (ev-free-loop' l P h) k))
dependent-uniqueness-circle l dup-circle {P} k =
  is-contr-is-equiv'
    ( fib (ev-free-loop' l P) k)
    ( tot (Î» h â†’ Eq-dependent-free-loops-eq l P (ev-free-loop' l P h) k))
    ( is-equiv-tot-is-fiberwise-equiv
      (Î» h â†’ is-equiv-Eq-dependent-free-loops-eq l P (ev-free-loop' l P h) k))
    ( is-contr-map-is-equiv (dup-circle P) k)

{- Now that we have established the dependent universal property, we can
   reduce the (non-dependent) universal property to the dependent case. We do
   so by constructing a commuting triangle relating ev-free-loop to 
   ev-free-loop' via a comparison equivalence. -}

tr-const :
  {i j : Level} {A : UU i} {B : UU j} {x y : A} (p : Id x y) (b : B) â†’
  Id (tr (Î» (a : A) â†’ B) p b) b
tr-const refl b = refl

apd-const :
  {i j : Level} {A : UU i} {B : UU j} (f : A â†’ B) {x y : A}
  (p : Id x y) â†’ Id (apd f p) ((tr-const p (f x)) âˆ™ (ap f p))
apd-const f refl = refl

comparison-free-loops :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (Y : UU l2) â†’
  free-loops Y â†’ dependent-free-loops l (Î» x â†’ Y)
comparison-free-loops l Y =
  tot (Î» y l' â†’ (tr-const (pr2 l) y) âˆ™ l')

abstract
  is-equiv-comparison-free-loops :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) (Y : UU l2) â†’
    is-equiv (comparison-free-loops l Y)
  is-equiv-comparison-free-loops l Y =
    is-equiv-tot-is-fiberwise-equiv
      ( Î» y â†’ is-equiv-concat (tr-const (pr2 l) y) y)

triangle-comparison-free-loops :
  { l1 l2 : Level} {X : UU l1} (l : free-loops X) (Y : UU l2) â†’
  ( (comparison-free-loops l Y) âˆ˜ (ev-free-loop l Y)) ~
  ( ev-free-loop' l (Î» x â†’ Y))
triangle-comparison-free-loops (pair x l) Y f =
  eq-Eq-dependent-free-loops
    ( pair x l)
    ( Î» x â†’ Y)
    ( comparison-free-loops (pair x l) Y (ev-free-loop (pair x l) Y f))
    ( ev-free-loop' (pair x l) (Î» x â†’ Y) f)
    ( pair refl (right-unit âˆ™ (inv (apd-const f l))))

abstract
  universal-property-dependent-universal-property-circle :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
    ( dependent-universal-property-circle l2 l) â†’
    ( universal-property-circle l2 l)
  universal-property-dependent-universal-property-circle l dup-circle Y =
    is-equiv-right-factor
      ( ev-free-loop' l (Î» x â†’ Y))
      ( comparison-free-loops l Y)
      ( ev-free-loop l Y)
      ( htpy-inv (triangle-comparison-free-loops l Y))
      ( is-equiv-comparison-free-loops l Y)
      ( dup-circle (Î» x â†’ Y))

{- Now we get the universal property of the circle from the induction principle
   of the circle by composing the earlier two proofs. -}

abstract
  universal-property-induction-principle-circle :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
    induction-principle-circle l2 l â†’ universal-property-circle l2 l
  universal-property-induction-principle-circle l =
    ( universal-property-dependent-universal-property-circle l) âˆ˜
    ( dependent-universal-property-induction-principle-circle l)

unique-mapping-property-circle :
  { l1 : Level} (l2 : Level) {X : UU l1} (l : free-loops X) â†’
  UU (l1 âŠ” (lsuc l2))
unique-mapping-property-circle l2 {X} l =
  ( Y : UU l2) (l' : free-loops Y) â†’
    is-contr (Î£ (X â†’ Y) (Î» f â†’ Eq-free-loops (ev-free-loop l Y f) l'))

abstract
  unique-mapping-property-universal-property-circle :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
    universal-property-circle l2 l â†’
    unique-mapping-property-circle l2 l
  unique-mapping-property-universal-property-circle l up-circle Y l' =
    is-contr-is-equiv'
      ( fib (ev-free-loop l Y) l')
      ( tot (Î» f â†’ Eq-free-loops-eq (ev-free-loop l Y f) l'))
      ( is-equiv-tot-is-fiberwise-equiv
        ( Î» f â†’ is-equiv-Eq-free-loops-eq (ev-free-loop l Y f) l'))
      ( is-contr-map-is-equiv (up-circle Y) l')

{- We assume that we have a circle. -}

postulate ğ•ŠÂ¹ : UU lzero

postulate base-ğ•ŠÂ¹ : ğ•ŠÂ¹

postulate loop-ğ•ŠÂ¹ : Id base-ğ•ŠÂ¹ base-ğ•ŠÂ¹

free-loop-ğ•ŠÂ¹ : free-loops ğ•ŠÂ¹
free-loop-ğ•ŠÂ¹ = pair base-ğ•ŠÂ¹ loop-ğ•ŠÂ¹

postulate ind-ğ•ŠÂ¹ : {l : Level} â†’ induction-principle-circle l free-loop-ğ•ŠÂ¹

dependent-universal-property-ğ•ŠÂ¹ :
  {l : Level} â†’ dependent-universal-property-circle l free-loop-ğ•ŠÂ¹
dependent-universal-property-ğ•ŠÂ¹ =
  dependent-universal-property-induction-principle-circle free-loop-ğ•ŠÂ¹ ind-ğ•ŠÂ¹

dependent-uniqueness-ğ•ŠÂ¹ :
  {l : Level} {P : ğ•ŠÂ¹ â†’ UU l} (k : dependent-free-loops free-loop-ğ•ŠÂ¹ P) â†’
  is-contr (Î£ ((x : ğ•ŠÂ¹) â†’ P x) (Î» h â†’ Eq-dependent-free-loops free-loop-ğ•ŠÂ¹ P (ev-free-loop' free-loop-ğ•ŠÂ¹ P h) k))
dependent-uniqueness-ğ•ŠÂ¹ {l} {P} k =
  dependent-uniqueness-circle free-loop-ğ•ŠÂ¹ dependent-universal-property-ğ•ŠÂ¹ k

universal-property-ğ•ŠÂ¹ :
  {l : Level} â†’ universal-property-circle l free-loop-ğ•ŠÂ¹
universal-property-ğ•ŠÂ¹ =
  universal-property-dependent-universal-property-circle
    free-loop-ğ•ŠÂ¹
    dependent-universal-property-ğ•ŠÂ¹

-- Section 14.3 Multiplication on the circle



{- Exercises -}

-- Exercise 11.1

{- The dependent universal property of the circle (and hence also the induction
   principle of the circle, implies that the circle is connected in the sense
   that for any family of propositions parametrized by the circle, if the
   proposition at the base holds, then it holds for any x : circle. -}

abstract
  is-connected-circle' :
    { l1 l2 : Level} {X : UU l1} (l : free-loops X) â†’
    ( dup-circle : dependent-universal-property-circle l2 l)
    ( P : X â†’ UU l2) (is-prop-P : (x : X) â†’ is-prop (P x)) â†’
    P (base-free-loop l) â†’ (x : X) â†’ P x
  is-connected-circle' l dup-circle P is-prop-P p =
    inv-is-equiv
      ( dup-circle P)
      ( pair p (center (is-prop-P _ (tr P (pr2 l) p) p)))
