{-# OPTIONS --without-K --exact-split --safe #-}

module 04-inductive-types where

import 03-natural-numbers
open 03-natural-numbers public

--------------------------------------------------------------------------------

-- Section 4.2 The unit type

-- Definition 4.2.1

data unit : UU lzero where
  star : unit
  
ğŸ™ = unit

ind-unit : {i : Level} {P : unit â†’ UU i} â†’ P star â†’ ((x : unit) â†’ P x)
ind-unit p star = p

--------------------------------------------------------------------------------

-- Section 4.3 The empty type

-- Definition 4.3.1

data empty : UU lzero where

ğŸ˜ = empty

ind-empty : {i : Level} {P : empty â†’ UU i} â†’ ((x : empty) â†’ P x)
ind-empty ()

ex-falso : {i : Level} {A : UU i} â†’ empty â†’ A
ex-falso = ind-empty

-- Definition 4.3.2

Â¬ : {i : Level} â†’ UU i â†’ UU i
Â¬ A = A â†’ empty

-- Proposition 4.3.3

functor-neg : {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  (P â†’ Q) â†’ (Â¬ Q â†’ Â¬ P)
functor-neg f nq p = nq (f p)

--------------------------------------------------------------------------------

-- Section 4.4 The booleans

-- Definition 4.4.1

data bool : UU lzero where
  true false : bool

-- Example 4.4.2

neg-ğŸš : bool â†’ bool
neg-ğŸš true = false
neg-ğŸš false = true

conjunction-ğŸš : bool â†’ (bool â†’ bool)
conjunction-ğŸš true true = true
conjunction-ğŸš true false = false
conjunction-ğŸš false true = false
conjunction-ğŸš false false = false

disjunction-ğŸš : bool â†’ (bool â†’ bool)
disjunction-ğŸš true true = true
disjunction-ğŸš true false = true
disjunction-ğŸš false true = true
disjunction-ğŸš false false = false

--------------------------------------------------------------------------------

-- Section 4.5 Coproducts

-- Definition 4.5.1

data coprod {i j : Level} (A : UU i) (B : UU j) : UU (i âŠ” j)  where
  inl : A â†’ coprod A B
  inr : B â†’ coprod A B

ind-coprod : {i j k : Level} {A : UU i} {B : UU j} (C : coprod A B â†’ UU k) â†’
  ((x : A) â†’ C (inl x)) â†’ ((y : B) â†’ C (inr y)) â†’
  (t : coprod A B) â†’ C t
ind-coprod C f g (inl x) = f x
ind-coprod C f g (inr x) = g x

-- Remark 4.5.2

functor-coprod :
  {l1 l2 l1' l2' : Level} {A : UU l1} {B : UU l2} {A' : UU l1'} {B' : UU l2'} â†’
  (A â†’ A') â†’ (B â†’ B') â†’ coprod A B â†’ coprod A' B'
functor-coprod f g (inl x) = inl (f x)
functor-coprod f g (inr y) = inr (g y)

-- Proposition 4.5.3

coprod-elim-left :
  {i j : Level} (A : UU i) (B : UU j) â†’
  Â¬ B â†’ coprod A B â†’ A
coprod-elim-left A B nb (inl a) = a
coprod-elim-left A B nb (inr b) = ex-falso (nb b)

coprod-elim-right :
  {i j : Level} (A : UU i) (B : UU j) â†’
  Â¬ A â†’ coprod A B â†’ B
coprod-elim-right A B na (inl a) = ex-falso (na a)
coprod-elim-right A B na (inr b) = b

--------------------------------------------------------------------------------

-- Section 4.6 Dependent pair types

-- Definition 4.6.1

data Î£ {i j : Level} (A : UU i) (B : A â†’ UU j) : UU (i âŠ” j) where
  pair : (x : A) â†’ (B x â†’ Î£ A B)

ind-Î£ : {i j k : Level} {A : UU i} {B : A â†’ UU j} {C : Î£ A B â†’ UU k} â†’
  ((x : A) (y : B x) â†’ C (pair x y)) â†’ ((t : Î£ A B) â†’ C t)
ind-Î£ f (pair x y) = f x y

-- Remark 4.6.2

ev-pair :
  {l1 l2 l3 : Level} {A : UU l1} {B : A â†’ UU l2} {C : Î£ A B â†’ UU l3} â†’
  ((t : Î£ A B) â†’ C t) â†’ (x : A) (y : B x) â†’ C (pair x y)
ev-pair f x y = f (pair x y)

-- Definition 4.6.3

pr1 : {i j : Level} {A : UU i} {B : A â†’ UU j} â†’ Î£ A B â†’ A
pr1 (pair a b) = a

pr2 : {i j : Level} {A : UU i} {B : A â†’ UU j} â†’ (t : Î£ A B) â†’ B (pr1 t)
pr2 (pair a b) = b

-- Definition 4.6.4

prod : {i j : Level} (A : UU i) (B : UU j) â†’ UU (i âŠ” j)
prod A B = Î£ A (Î» a â†’ B)

pair' :
  {i j : Level} {A : UU i} {B : UU j} â†’ A â†’ B â†’ prod A B
pair' = pair

_Ã—_ :  {i j : Level} (A : UU i) (B : UU j) â†’ UU (i âŠ” j)
A Ã— B = prod A B

--------------------------------------------------------------------------------

-- Section 4.7 The type of integers

-- Definition 4.7.1

-- The type of integers

â„¤ : UU lzero
â„¤ = coprod â„• (coprod unit â„•)

-- Inclusion of the negative integers

in-neg : â„• â†’ â„¤
in-neg n = inl n

-- Negative one

neg-one-â„¤ : â„¤
neg-one-â„¤ = in-neg zero-â„•

-- Zero

zero-â„¤ : â„¤
zero-â„¤ = inr (inl star)

-- One

one-â„¤ : â„¤
one-â„¤ = inr (inr zero-â„•)

-- Inclusion of the positive integers

in-pos : â„• â†’ â„¤
in-pos n = inr (inr n)

-- Proposition 4.7.2

ind-â„¤ :
  {i : Level} (P : â„¤ â†’ UU i) â†’
  P neg-one-â„¤ â†’ ((n : â„•) â†’ P (inl n) â†’ P (inl (succ-â„• n))) â†’
  P zero-â„¤ â†’
  P one-â„¤ â†’ ((n : â„•) â†’ P (inr (inr (n))) â†’ P (inr (inr (succ-â„• n)))) â†’
  (k : â„¤) â†’ P k
ind-â„¤ P p-1 p-S p0 p1 pS (inl zero-â„•) = p-1
ind-â„¤ P p-1 p-S p0 p1 pS (inl (succ-â„• x)) =
  p-S x (ind-â„¤ P p-1 p-S p0 p1 pS (inl x))
ind-â„¤ P p-1 p-S p0 p1 pS (inr (inl star)) = p0
ind-â„¤ P p-1 p-S p0 p1 pS (inr (inr zero-â„•)) = p1
ind-â„¤ P p-1 p-S p0 p1 pS (inr (inr (succ-â„• x))) =
  pS x (ind-â„¤ P p-1 p-S p0 p1 pS (inr (inr (x))))

-- Definition 4.7.3

succ-â„¤ : â„¤ â†’ â„¤
succ-â„¤ (inl zero-â„•) = zero-â„¤
succ-â„¤ (inl (succ-â„• x)) = inl x
succ-â„¤ (inr (inl star)) = one-â„¤
succ-â„¤ (inr (inr x)) = inr (inr (succ-â„• x))

--------------------------------------------------------------------------------

-- Exercises

-- Exercise 4.1 (a)

pred-â„¤ : â„¤ â†’ â„¤
pred-â„¤ (inl x) = inl (succ-â„• x)
pred-â„¤ (inr (inl star)) = inl zero-â„•
pred-â„¤ (inr (inr zero-â„•)) = inr (inl star)
pred-â„¤ (inr (inr (succ-â„• x))) = inr (inr x)

-- Exercise 4.1 (b)

-- Addition on â„¤

add-â„¤ : â„¤ â†’ â„¤ â†’ â„¤
add-â„¤ (inl zero-â„•) l = pred-â„¤ l
add-â„¤ (inl (succ-â„• x)) l = pred-â„¤ (add-â„¤ (inl x) l)
add-â„¤ (inr (inl star)) l = l
add-â„¤ (inr (inr zero-â„•)) l = succ-â„¤ l
add-â„¤ (inr (inr (succ-â„• x))) l = succ-â„¤ (add-â„¤ (inr (inr x)) l)

-- The negative of an integer

neg-â„¤ : â„¤ â†’ â„¤
neg-â„¤ (inl x) = inr (inr x)
neg-â„¤ (inr (inl star)) = inr (inl star)
neg-â„¤ (inr (inr x)) = inl x

-- Exercise 4.1 (c)

-- Multiplication on â„¤

mul-â„¤ : â„¤ â†’ â„¤ â†’ â„¤
mul-â„¤ (inl zero-â„•) l = neg-â„¤ l
mul-â„¤ (inl (succ-â„• x)) l = add-â„¤ (neg-â„¤ l) (mul-â„¤ (inl x) l)
mul-â„¤ (inr (inl star)) l = zero-â„¤
mul-â„¤ (inr (inr zero-â„•)) l = l
mul-â„¤ (inr (inr (succ-â„• x))) l = add-â„¤ l (mul-â„¤ (inr (inr x)) l)

-- Exercise 4.2

Â¬Â¬ : {l : Level} â†’ UU l â†’ UU l
Â¬Â¬ P = Â¬ (Â¬ P)

Â¬Â¬Â¬ : {l : Level} â†’ UU l â†’ UU l
Â¬Â¬Â¬ P = Â¬ (Â¬ (Â¬ P))

-- Exercise 4.2 (a)

no-fixed-points-neg :
  {l1 : Level} (A : UU l1) â†’ Â¬ ((A â†’ Â¬ A) Ã— (Â¬ A â†’ A))
no-fixed-points-neg A (pair f g) =
  ( Î» (h : Â¬ A) â†’ h (g h)) (Î» (a : A) â†’ f a a)

-- Exercise 4.2 (b)

intro-dn : {l : Level} {P : UU l} â†’ P â†’ Â¬Â¬ P
intro-dn p f = f p

-- Exercise 4.2 (c)

functor-dn : {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  (P â†’ Q) â†’ (Â¬Â¬ P â†’ Â¬Â¬ Q)
functor-dn f = functor-neg (functor-neg f)

-- Exercise 4.2 (d)

{- In this exercise we were asked to show that (A + Â¬A) implies (Â¬Â¬A â†’ A). In 
   other words, we get double negation elimination for the types that are 
   decidable. -}

double-negation-elim-is-decidable :
  {i : Level} (P : UU i) â†’ coprod P (Â¬ P) â†’ (Â¬Â¬ P â†’ P)
double-negation-elim-is-decidable P (inl x) p = x
double-negation-elim-is-decidable P (inr x) p = ind-empty (p x)

-- Exercise 4.2 (e)

dn-is-decidable : {l : Level} {P : UU l} â†’ Â¬Â¬ (coprod P (Â¬ P))
dn-is-decidable {P = P} f =
  functor-neg (inr {A = P} {B = Â¬ P}) f
    ( functor-neg (inl {A = P} {B = Â¬ P}) f)

-- Exercise 4.2 (f)

dn-dn-elim : {l : Level} {P : UU l} â†’ Â¬Â¬ (Â¬Â¬ P â†’ P)
dn-dn-elim {P = P} f =
  ( Î» (np : Â¬ P) â†’ f (Î» (nnp : Â¬Â¬ P) â†’ ind-empty {P = Î» x â†’ P} (nnp np)))
    ( Î» (p : P) â†’ f (Î» (nnp : Â¬Â¬ P) â†’ p))

-- Exercise 4.2 (g)

tautology-4-2-f :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  Â¬Â¬ (((P â†’ Q) â†’ P) â†’ P)
tautology-4-2-f {P = P} {Q} f =
  ( Î» (np : Â¬ P) â†’ f (Î» h â†’ h (Î» p â†’ ex-falso (np p))))
  ( Î» (p : P) â†’ f (Î» h â†’ p))

-- Exercise 4.2 (h)

dn-linearity-implication :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  Â¬Â¬ (coprod (P â†’ Q) (Q â†’ P))
dn-linearity-implication {P = P} {Q = Q} f =
  ( Î» (np : Â¬ P) â†’
    functor-neg (inl {A = P â†’ Q} {B = Q â†’ P}) f (Î» p â†’ ind-empty (np p)))
    ( Î» (p : P) â†’
      functor-neg (inr {A = P â†’ Q} {B = Q â†’ P}) f (Î» q â†’ p))

-- Exercise 4.2 (i)

dn-elim-neg : {l : Level} (P : UU l) â†’ Â¬Â¬Â¬ P â†’ Â¬ P
dn-elim-neg P f p = f (Î» g â†’ g p)

-- Exercise 4.2 (j)

dn-extend :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  (P â†’ Â¬Â¬ Q) â†’ (Â¬Â¬ P â†’ Â¬Â¬ Q)
dn-extend {P = P} {Q = Q} f = dn-elim-neg (Â¬ Q) âˆ˜ (functor-dn f)

-- Exercise 4.2 (k)

dn-elim-exp :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  Â¬Â¬ (P â†’ Â¬Â¬ Q) â†’ (P â†’ Â¬Â¬ Q)
dn-elim-exp {P = P} {Q = Q} f p =
  dn-elim-neg (Â¬ Q) (functor-dn (Î» (g : P â†’ Â¬Â¬ Q) â†’ g p) f)

-- Exercise 4.2 (l)

dn-elim-prod :
  {l1 l2 : Level} {P : UU l1} {Q : UU l2} â†’
  Â¬Â¬ ((Â¬Â¬ P) Ã— (Â¬Â¬ Q)) â†’ (Â¬Â¬ P) Ã— (Â¬Â¬ Q)
dn-elim-prod {P = P} {Q = Q} f =
  pair
    ( dn-elim-neg (Â¬ P) (functor-dn pr1 f))
    ( dn-elim-neg (Â¬ Q) (functor-dn pr2 f))

-- Exercise 4.3

-- Exercise 4.3 (a)

data list {l : Level} (A : UU l) : UU l where
  nil : list A
  cons : A â†’ list A â†’ list A

in-list : {l : Level} {A : UU l} â†’ A â†’ list A
in-list a = cons a nil

-- Exercise 4.3 (b)

fold-list :
  {l1 l2 : Level} {A : UU l1} {B : UU l2} (b : B) (Î¼ : A â†’ (B â†’ B)) â†’
  list A â†’ B
fold-list b Î¼ nil = b
fold-list b Î¼ (cons a l) = Î¼ a (fold-list b Î¼ l)

-- Exercise 4.3 (c)

length-list :
  {l : Level} {A : UU l} â†’ list A â†’ â„•
length-list = fold-list zero-â„• (Î» a â†’ succ-â„•)

-- Exercise 4.3 (d)

sum-list-â„• :
  list â„• â†’ â„•
sum-list-â„• = fold-list zero-â„• add-â„•

-- Exercise 4.3 (e)

concat-list :
  {l : Level} {A : UU l} â†’ list A â†’ (list A â†’ list A)
concat-list {l} {A} = fold-list id (Î» a f â†’ (cons a) âˆ˜ f)

-- Exercise 4.3 (f)

flatten-list :
  {l : Level} {A : UU l} â†’ list (list A) â†’ list A
flatten-list = fold-list nil concat-list

-- Exercise 4.3 (g)

reverse-list :
  {l : Level} {A : UU l} â†’ list A â†’ list A
reverse-list nil = nil
reverse-list (cons a l) = concat-list (reverse-list l) (in-list a)
